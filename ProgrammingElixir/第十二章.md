
第十二章 流程控制


本章内容：

* if 和 unless 语句
* cond（多分支if）语句
* case（switch模式匹配）语句
* 异常

我们已经长时间探索了Elixir。到目前为止，还没有出现过if语句，或者任何看起来像流程控制的语句。

情况挺不错。Elixir可以让我们写短小的函数，联合卫语句喝参数的模式匹配可以实现其他语言中的流程控制。

Elixir代码尝试接近声明式语言，但不是必须的。


Elixir也有流程控制结构。我之所以迟迟不介绍是因为我觉得尽量不使用它们。你应该明确的放弃使用代码中的if或者case语句。思考更多函数式的方案。如果你不使用严格的控制结构来书写函数的好处是显而易见，你代码将会更精炼，更易读，更易测试和重用。如果你的Elixir代码有10行或者20行才写完一个函数。可以肯定需要使用本章的一些结构知识来作简化。


### if 和 unless 语句


Elixir中，if和它的好基友unless语句，它们都接受两个参数：条件表达式和关键字列表。键是 do:和 else:。如果条件表达式为真值，则会执行分支键do:的值，否则则计算分支键else:的值。没有其他的分支了。

```elixir
iex> if 1 == 1, do: "true part", else: "false part"
"true part"
iex> if 1 == 2, do: "true part", else: "false part"
"false part"
```

本质是函数，但是Elixir提供了语法糖（syntactic sugar）。可以这么写：

```elixir
iex> if 1 == 1 do
...> "true part"
...> else
...> "false part"
...> end
true part
```

unless语句也类似:

```elixir
iex> unless 1 == 1, do: "error", else: "OK"
"OK"
iex> unless 1 == 2, do: "OK", else: "error"
"OK"
iex> unless 1 == 2 do
...> "OK"
...> else
...> "error"
...> end
"OK"
```

### cond 宏

cond 宏可以让你列出一些条件表达式，每一个表达式都可以联系一个分支的代码。当遇到表达式求值为真的时候，分支的代码将会被展开。

FizzBuz游戏，孩纸们会从一开始数数。如果数到3的倍数，就需要说“Fizz.”。如果是5的倍数，就需要说“Buzz.”。3或者5共同的倍数则需要说“FizzBuzz.”。其他情况就直接说数到的数。

用elixir实现：

control/fizzbuzz.ex

```elixir

defmodule FizzBuzz do
	
	def upto(n) when n > 0, do: _upto(1, n, [])

	defp _upto(_current, 0, result), do: Enum.reverse result

	defp _upto(current, left, result) do
		
		next_answer = cond do
			rem(current, 3) == 0 and rem(current, 5) == 0 -> "FizzBuzz"
			rem(current, 3) == 0 						  -> "Fizz"
			rem(current, 5) == 5 						  -> "Buzz"
			true 										  -> current	
		end

		_upto(current+1, left-1, [next_answer|result])

	end
end

```

首先，看代码的cond语句哪一行。我们把cond表达式的返回的结果绑定给变了next_answer。在cond语句块内，条件语句中变量current分别和3，5取余。Elixir会依次检测条件语句，如果条件表达式的值为真，则会执行->后面的语句。_upto函数递归的执行来作循环下一个值。注意->后面的语句，如果之前的条件已经符合返回真了，那么后面的语句将不会被执行，这一点和传统的case不太一样。

还有一个小问题。函数运算的结果得到的总是把最近计算的值放在列表的头部。因此，当递归结束之后，需要将列表元素翻转。这也就是另外一个函数子句调用了Enum的reverse方法。这是普遍的做法，当然不用担心性能了，reverse方法经过了高度优化。

打开iex测试：

```elixir
iex> c("fizzbuzz.ex")
[FizzBuzz]
iex> FizzBuzz.upto(20)
[1, 2, "Fizz", 4, "Buzz", "Fizz", 7, 8, "Fizz", "Buzz", 11, "Fizz",
.. 13, 14, "FizzBuzz", 16, 17, "Fizz", 19, "Buzz"]
```
当然，我们也可以换一种方式移除reverse的调用。在开始处的时候，倒叙的处理数数字，即从n到1，而不是从1到n进行递归。处理过程方向，也就可以得到正确的顺序了。

control/fizzbuzz1.ex

```elixir
defmodule FizzBuzz do

	def upto(n) when n > 0, do: _downto(n, [])

	defp _downto(0, result), do: result

	defp _downto(current, result) do

		next_answer = cond do
					rem(current, 3) == 0 and rem(current, 5) == 0 -> "FizzBuzz"
					rem(current, 3) == 0 						  -> "Fizz"
					rem(current, 5) == 0 						  -> "Buzz"
					true 										  -> current
				end
		_downto(current-1, [ next_answer | result ])
	end
end
```

这段代码比前一个例子更清晰。可是，这样的处理还是略微不符合正常的思维方式，人们希望是按照正常的顺序遍历。

This code is quite a bit cleaner than the previous version. However, it is also slightly less idiomatic—readers will expect to traverse the numbers in a natural order and reverse the result.

因此有了第三个可选的方式。FizzBuzz代码将数字转换成某个值。如果可能的话，可以让他们可变。然后使用Enum.map模块改变从1到n的数字排列。

control/fizzbuzz2.ex

```elixir
defmodule FizzBuzz do

	def upto(n) when n > 0 do

		1..n |> Enum.map(&fizzbuzz/1)
	end

	defp fizzbuzz(n) do

		cond do

			rem(n, 3) == 0 and rem(n, 5) == 0 -> "FizzBuzz"
			rem(n, 3) == 0 					  -> "Fizz"
			rem(n, 5) == 0 					  -> "Buzz"
			true 							  -> n
		end
	end
end
```

本章节主要是介绍cond宏的用法，但是你会发现实际上并不常用。相反，模式匹配更优：

control/fizzbuzz3.ex

```elixir
defmodule FizzBuzz do

	def upto(n) when n > 0 do

		1..n |> Enum.map(&fizzbuzz/1)
	end

	defp fizzbuzz(n) when rem(n, 3) == 0 and rem(n, 5) == 0, do: "FizzBuzz"

	defp fizzbuzz(n) when rem(n, 3) == 0, do: "Fizz"

	defp fizzbuzz(n) when rem(n, 5) == 0, do: "Buzz"

	defp fizzbuzz(n), do: n
end
```

如何选择完全取决于你自己。

### case 语句

case将一个值与许多模式进行比较，直到找到一个匹配的，返回匹配条件之后的代码执行结果。模式匹配也可以使用卫语句。

例如：File.open函数返回两个元素的元组。如果打开成功，返回{:ok, file}，file是文件标示符。如果打开失败，返回{:error, reason}。这种情况下使用case语句就很合适。

control/case.ex

```elixir
case File.open("case.ex") do

	{ :ok, file } -> IO.puts "First line: #{IO.read(file, :line)}"

	{ :error, reason } -> IO.puts "Failed to open file: #{reason}"
end
```

处理结果:

第一行: `case File.open("case.ex") do`

如果更改一个不存在的文件名，然后会打开失败，可以使用模式匹配处理：

control/case1.exs

```elixir
defmodule Users do

	dave = %{ name: "Dave", state: "TX", likes: "programming" }
	case dave do
		%{state: some_state} = person -> IO.puts "#{person.name} lives in #{some_state}"
		_ -> IO.puts "No matches"
	end

end
```

我们已经了解如何使用卫语句提升模式匹配，例如：

control/case2.exs

```elixir
defmodule Bouncer do

	dave = %{name: "Dave", age: 27}

	case dave do

		person = %{age: age} when is_number(age) and age >= 21 -> IO.puts "You are cleared to enter the Foo Bar, #{person.name}"

		_ -> IO.puts "Sorry, no admission"
	end
end
```

### 异常

首先，官方警告：Elixir的异常不属于控制结构。实际上，Elixir的异常是那些正常操作中不会出现的地方。这意味着当数据库挂掉了或者服务停止响应了可以考虑使用异常。打开配置文件失败也可以视为异常。可是打开一个用户输入的文件失败则不是异常。（因为这个错误是可以预料避免的）

使用raise函数抛出一个异常。传入一个字符串参数，将会引发一个RuntimeError错误。

```elixir
iex> raise "Giving up"
** (RuntimeError) Giving up
```

也可以自定义异常类型，这是可选的属性。所有异常都可以实现一个message属性输出

```elixir
iex> raise RuntimeError
** (RuntimeError) runtime error
iex> raise RuntimeError, message: "override message"
** (RuntimeError) override message
```

在Elixir使用异常的频率要远小于别的语言。Elixir的设计哲学就是错误应该被显示传出来，由supervising处理。当讨论OTP的时候再介绍Supervisors。

Elixir有一些常见的异常捕获（exception-catching）语法。为了强调少用，我才最后介绍异常。

### 设计异常

如果文件打开成功，返回{:ok, file}元组，`file`则是文件资源句柄。如果打开失败了，返回{:error, reason}元组。因此，在代码层面上打开文件不成功是可以控制的，例如可以这么写：

```elixir
case File.open(user_file_name) do

	{:ok, file} -> process(file)

	{:error, message} -> IO.puts :stderr, "Couldn't open #{user_file_name}: #{message}"

end
```

如果希望每次都成功的打开文件，当打开失败的时候触发异常：

```elixir
case File.open("config_file") do 
	
	{:ok, file} -> process(file)
	{:error, message} -> raise "Failed to open config file: #{message}"
end
```

或者让Elixir自己根据你写的代码触发异常。

```elixir
{ :ok, file } = File.open("config_file")

process(file)
```

第一行模式匹配失败，Elixir将会触发一个MatchError异常。这不会提供一个处理错误的信息。如果这个错误不能发生，这样的处理到很合适。

更好的处理方式是使用 File.open!。正如你所见，Elixir函数后面的感叹号，表示该函数可能引发异常，并且异常将会有意义，可以这么使用, `file = File.open!("config_file")`。

### 尽量少用

Elixir还有其他一些控制流程语句：if，unless，cond，case，和raise（尽量不要用raise做控制流程）。令人奇怪的是实际上他们并不常用。Elixir程序表达力很丰富，也不需要很多分支结构的代码。本章之后，对elixir的基本介绍即将完成。下一章，我们将会实现一个完整的应用程序。



