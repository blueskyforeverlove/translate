

## 第七章 列表和递归

本章内容：

* 列表递归
* 列表创建喝遍历
* 累加
* map和reduce



常见的编程语言中，对列表进行迭代或者循环再自然不过了。为什么还会有一章专门讨论列表和递归呢？因为如果你认真的思考问题，你会发现，递归是处理列表的一大神技。


### 头（Heads）和尾（Tails）

之前我们说过，列表可以为空，也可是由头部（head）和尾部（tail）组成。列表是递归定义，head指列表的第一个元素，tail指列表除去head剩余的列表。

使用中括号可以表示一个空列表：[].

发挥你的想象，我们可以使用`|`来分隔一个列表的`head`和`tail`。只有一个元素的列表，例如`[3]`可以表示成一个 *3*（`head`） 和一个 *空列表* （`tail`）的合成：

```elixir
[ 3 | [] ]
```

`|`管道符，它把列表分为左右两部分，分别是`head`和`tail`。


在看列表`[2, 3]`。它的`head`是 *2*，`tail`是 *3*。按照前一个列子的方法，`[2, 3]`可以写成：

```elixir
[ 2 | [ 3 | [] ] ]
```


此时此刻，你的大脑会高数你赶紧去看今天的`XKCD`，这样的列表一点用的都没有。排除那些干扰的声音啦，就一会。我们将会召唤魔法了。但是在此之前，需要给列表再加一个元素，新列表为 `[1, 2, 3]`。该列表的`head`是 *1*，tail是`[2, 3]`。

```elixir
[ 1 | [ 2 | [ 3 | [] ] ] ]
```

这是合法的语法，打开iex输入:

```elixir
iex> [ 1 | [ 2 | [ 3 | [] ] ] ]
[1, 2, 3]
```

这就是魔法。当我们讨论模式匹配的时候，我说过，模式可以是一个列表，它可以与右边的值进行匹配。

```elixir
iex> [a, b, c ] = [ 1, 2, 3 ]
[1, 2, 3]
iex> a
1
iex> b
2
iex> c
3
```

模式匹配中也可以使用`|``管道符。其左边匹配列表的head，右边匹配tial:

```elixir
iex> [ head | tail ] = [ 1, 2, 3 ]
[1, 2, 3]
iex> head
1
iex> tail
[2, 3]
```



### iex 是如何相识列表的?

第十一章介绍字符串（String）和二进制（Binaries），Elixir中的字串有两种形式。双引号引起来的字符是内存中一段连续的字符（characters），

第二种形式是单引号包裹的字符，它们其实是字符的码值（codepoints）列表。因此字符`'cat'`是由 *99*, *97*, *116* 三个码值构成。

在iex中会比较头疼。当你看着列表`[99,97,116]`，很难看出它是字串`'cat'`还是三个整数的列表。因此iex采取了启发手段。当列表的所有字符是可以打印的，那么就会显示字符串，否则就相识码值列表。

```elixir
iex≻ [99, 97, 116]
'cat'
iex≻ [99, 97, 116, 0] 			# '0' 不能打印
[99, 97, 116, 0]
```

十一章字符串和二进制，我们将会再深入讨论这种行为。现在，当年你的定义列表而iex弹出字符串的时候不要奇怪啦。



### 使用 head 和 tail 处理列表 

我们构建一个列表，尝试把它分为head和一个tail新列表。

为什么要再模块和函数之后才讨论列表呢？因为列表和函数递归是关系就像鱼和水的一样关系紧密。现在实现一个计算列表长度的功能吧：

* 空列表的长度为0
* 列表的长度等于1加上列表tail的长度

使用elxiir实现：


lists/mylist.exs
```
defmodule MyList do
	def len([]), do: 0
	def len([head|tail]), do: 1 + len(tail)
end
```

唯一的难点在于定义第二个函数子句：

```elixir
def len([ head | tail ]) ...
```

对非空列表进行模式匹配。匹配的时候，列表的第一个元素和变量head绑定，剩下的元素和变量tail绑定（记住，当列表为空的时候将会停止匹配，即tail为[]的时候）。

下面分解一下列表`[11, 12, 13, 14, 15]`在函数运行的每一步过程，第一步分解head和tail，使用1 加上剩下的tail长度：


```
len([11,12,13,14,15])
= 1 + len([12,13,14,15])
= 1 + 1 + len([13,14,15])
= 1 + 1 + 1 + len([14,15])
= 1 + 1 + 1 + 1 + len([15])
= 1 + 1 + 1 + 1 + 1 + len([])
= 1 + 1 + 1 + 1 + 1 + 0
= 5
```

实践检验一下我们的代码：


```elixir
iex> c "mylist.exs"
...mylist.exs:3: variable head is unused
[MyList]
iex> MyList.len([])
0
iex> MyList.len([11,12,13,14,15])
5
```

正常工作，很好。但是编译的时候，抛出了一个警告，因为定义的head变量并没有被使用。修复也很简单，使用`_`下划线可以忽略不需要的变量，仅仅只是一个占位符。


lists/mylist1.exs

```elixir 
defmodule MyList do

	def len([]), do: 0
	def len([_head | tail]), do: 1 + len(tail)
	
end
```

重新编译，之前的警告没有了（可是，如果你第二次编译，你会得到另外一个警告“ redefining module MyList.”，重新定义了模块，这恰巧是Elixir小心谨慎的地方）

```elixir
iex> c "mylist1.exs"
[MyList]
iex> MyList.len([1,2,3,4,5])
5
iex> MyList.len(["cat", "dog"])
2
```

#### 使用 Head 和 Tail 构建列表


再接再厉，实现一个函数，接受一个列表参数，返回列表元素平方的新列表。例子不会列出完成的代码，代码还是定义在之前的`MyList`模块中。


lists/mylist1.exs

```elixir
def square([]), do: []

def square([ head | tail ]), do: [ head*head | square(tail) ]
```

上面使用了很多技巧。首先，观察两个函数子句的参数模式匹配。第一个函数子句只批评空列表，第二个子句匹配其他列表。

其次，再观察第二个函数子句的函数体：

```elixir
def square([ head | tail ]), do: [ head*head | square(tail) ]
```

匹配非空列表的时候，返回一个列表，其中head进行平方，tail作为参数递归调用，重复这样的过程。

编译实践一下：

```elixir
iex> c "mylist1.exs"
[MyList]
iex> MyList.square [] 
[]
iex> MyList.square [4,5,6] # and this calls the 2nd
[16, 25, 36]
```

再实现一个类似的功能，给每一个列表元素加 *1*：


lists/mylist1.exs

```elixir

def add_1([]), do: []
def add_1([ head | tail ]), do: [ head+1 | add_1(tail) ]
```

编译：

```elixir
iex> c "mylist1.exs"
[MyList]
iex> MyList.add_1 [1000]
[1001]
iex> MyList.add_1 [4,6,8]
[5, 7, 9]
```


### 创建 Map 函数


add_1函数实现了给每一个列表元素加1。第二个函数子句完成了所有计算任务。每一次递归都好像执行同样的功能。也就是通过模式匹配将参数分为head和tail，然后对head进行计算，再将结果列表和递归调用tail的结果联合起来返回一个新的列表。可以据此写一个map函数，用来实现遍历列表操作，返回计算后结果的新列表的函数。


lists/mylist1.exs

```
def map([], _func), do: []
def map([ head | tail ], func), do: [ func.(head) | map(tail, func) ]
```

map函数看起来几乎和add_1函数一模一样。当参数是一个空列表的时候返回空列表；否则将会返回一个新列表，这个新列表由原来的旧列表的每一个元素一次进行func函数调用得到的结果组成。第一个函数子句中，使用了_func来忽视第二个参数。

调用map函数很简单，传入一个列表和一个匿名函数即可：

```elixir
iex> c "mylist1.exs"
[MyList]
iex> MyList.map [1,2,3,4], fn (n) -> n*n end
[1, 4, 9, 16]
```

函数式内建的类型，使用关键字 fn和 end包裹定义。这里我们把匿名函数当成函数的参数（func）进行传递。函数子句内调用了func.(head)，这里进行了平方计算，返回了计算结果，并组合成新列表返回。

当然还可以改变函数参数，实现其他功能：

```elxir
iex> MyList.map [1,2,3,4], fn (n) -> n+1 end
[2, 3, 4, 5]
```

```
iex> MyList.map [1,2,3,4], fn (n) -> n > 2 end
[false, false, true, true]
```

还可以使用&对函数缩写：

```elixir
iex> MyList.map [1,2,3,4], &(&1 + 1)
[2, 3, 4, 5]
iex> MyList.map [1,2,3,4], &(&1 > 2)
[false, false, true, true]
```

### 递归累积


目前为止，你已经学会了如何处理列表的元素，但是，当我们需要对列表的元素求和呢？与上面的例子不同，我们不仅要处理每一个元素，还需要把他们联系起来。


递归定义如下：

```elixir
sum([]) → 0
sum([ head |tail ]) → «total» + sum(tail)
```

但是，基本的模式需要我们在整个递归过程中存储total的值。记住，我们的目标是有一个不可变得状态，因此我们无法在模块内维护一个变量用来同步数据。

但是，我们可以把状态传递给函数参数。

lists/sum.exs

```
defmodule MyList do

	def sum([], total), do: total

	def sum([ head | tail ], total), do: sum(tail, head+total)
end
```

函数有两个参数，第一个是列表，另外一个是列表元素的和。递归调用的时候，我们传递tail和head增量的累加和。

整个过程中，这些类型的功能保持不变，条件是任何一次调用都返回真实的值。这种情况下，递归的算法保持不变，列表元素在参数传递的时候进行累加，累加和与实际的列表元素之和相等。当列表变成空的时候，返回这个累加和即可。

调用sum函数的时候，记得添加一个初始化total参数（传0即可）：

```elixir
iex> c "sum.exs"
[MyList]
iex> MyList.sum([1,2,3,4,5], 0)
15
iex> MyList.sum([11,12,13,14,15], 0)
65
```

是不是觉得多写一个0很乏味。Elixir可以使用公有函数提供模块接口，使用私有函数隐藏算法细节：

lists/sum2.exs

```elixir
defmodule MyList do

	def sum(list), do: _sum(list, 0)

	# 私有函数
	defp _sum([], total), do: total
	defp _sum([ head | tail ], total), do: _sum(tail, head+total)
end
```

两个需要注意的地方：首先，我们使用defp定义了私有函数。在模块外部，私有函数式不能调用。其次，我们选择定义一个同名但没有下划线的公共的辅助函数。Elixir会把公共的和私有的函数进行区分，但是程序员可能会被误解他们是有联系的两个函数。

（即使我们使用同名的公有和私有函数，它们仍然是不同的函数。至少参数的个数不一样。使用下划线区分会比较清晰。很多库喜欢使用do_xxx的形式来辅助）


### 总结sum函数


sum函数将容器递减到只剩一个值。类似的函数还有遍历列表元素返回最大值或最小值，消除元素之间空格的等待。我们能否写一个通用的reduce函数来处理这样容器递减的任务呢？

类似sum/1函数，首先需要一个容器参数。另外，我们还需要传一个初始值，初始值需要和当前值计算，并把结果返回给容器的下一个元素，看起来我们的reduce函数需要三个参数：

```elixir
reduce(collection, initial_value, fun)
```

思考一下递归设计：

```elixir
reduce([], value, _) → value

reduce([ head |tail ], value, fun) → reduce(tail, fun.(head, value), fun)
```

reduce函数可以把列表的head和当前值计算，并把结果作为下一次使用tail递减调用的新当前值。

reduce代码如下：


lists/reduce.exs

```elixir
defmodule MyList do

	def reduce([], value, _) do
		value
	end

	def reduce([head | tail], value, func) do
		reduce(tail, func.(head, value), func)
	end
end
```

使用函数的缩写调用：

```elixir
iex> c "reduce.exs"
[MyList]
iex> MyList.reduce([1,2,3,4,5], 0, &(&1 + &2))
15
iex> MyList.reduce([1,2,3,4,5], 1, &(&1 * &2))
120
```

### 更多复杂的列表模式


并不是所有列表问题都是每次处理一个元素这么简单。很幸运，我们有 `|`连接符。可以将列表左边的多个值联合起来：

```elixir
iex> [ 1, 2, 3 | [ 4, 5, 6 ]]
[1, 2, 3, 4, 5, 6]
```




The same thing works in patterns, so you can match multiple individual elements as the head. For example, the following program swaps pairs of values in a list.


与模式匹配的相同，可以把多个值匹配成为列表的head。例如下面的程序将会交换列表相邻两个元素的位置：


lists/swap.exs

```elixir
defmodule Swapper do

	def swap([]), do: []
	def swap([ a, b | tail ]), do: [ b, a | swap(tail) ]
	def swap([_]), do: raise "Can't swap a list with an odd number of elements"
end
```
在iex中测试：

```elixir
iex> c "swap.exs"
[Swapper]
iex> Swapper.swap [1,2,3,4,5,6]
[2, 1, 4, 3, 6, 5]
iex> Swapper.swap [1,2,3,4,5,6,7]
** (RuntimeError) Can't swap a list with an odd number of elements
```

swap的第三个函数子句的匹配奇数个数的列表。递归到最后的时候，如果还剩一个元素剩下，将会触发该子句的代码。为了列表元素两两交互，必须保证初始化的参数列表是偶数个数。

### 列表中的列表


想象一下，很多气象站都会记录温度和降雨量，一个包含下面列表元素的列表，大概记录如下：

```elixir
[ timestamp, location_id, temperature, rainfall ]
```
代码的功能是时下一个列表的解析，最终结果找出location_id为27的列表


lists/weather.exs

```elixir
defmodule WeatherHistory do

	def for_location_27([]), do: []

	def for_location_27([ [time, 27, temp, rain ] | tail]) do

		[ [time, 27, temp, rain] | for_location_27(tail) ]
	end

	def for_location_27([ _ | tail]), do: for_location_27(tail)

end
```

这是一个标准的递归，直到列表为空。但是，仔细看定义的第二个函数子句。正常情况下`|`之前会匹配一个head元素，模式如下

```elixir
for_location_27([ [ time, 27, temp, rain ] | tail])
```
为了匹配成功，head必须是一个包含四个元素的列表，并且第二个元素的值是 *27*。此功能将仅用于从所需位置的条目执行。当我们做这样的过滤的时候，我们不需要记住那么不匹配的结果。也就是第三个子句定义的功能。可以这么写


```elixir
for_location_27([ [ time, _, temp, rain ] | tail])
```

实际上，此时我们并不需要head，因此可以全部忽略（即 [ _ | tail]）：

使用一些数据做测试：

lists/weather.exs

```elixir
def test_data do
	[
		[1366225622, 26, 15, 0.125],
		[1366225622, 27, 15, 0.45],
		[1366225622, 28, 21, 0.25],
		[1366229222, 26, 19, 0.081],
		[1366229222, 27, 17, 0.468],
		[1366229222, 28, 15, 0.60],
		[1366232822, 26, 22, 0.095],
		[1366232822, 27, 21, 0.05],
		[1366232822, 28, 24, 0.03],
		[1366236422, 26, 17, 0.025]
	]

end
```

可以使用iex测试。为了更简单，我使用了import指令将WeatherHistory模块导入到当前的作用域。不需要在每个函数调用的时候写上模块名了。


We can use that to play with our function in iex. To make this easier, I’m using the import function. This adds the functions in WeatherHistory to our local name scope. After calling import we don’t have to put the module name in front of every function call.

```elixir
iex> c "weather.exs"
[WeatherHistory]
iex> import WeatherHistory
nil
iex> for_location_27(test_data)
[[1366225622, 27, 15, 0.45], [1366229222, 27, 17, 0.468],
[1366232822, 27, 21, 0.05]]
```


我们的功能是针对被限制的特定的位置。因此可以把这个位置当成参数传递，只要匹配太就可以了。


lists/weather2.exs

```elixir
defmodule WeatherHistory do

	def for_location([], _target_loc), do: []

	def for_location([ [time, target_loc, temp, rain ] | tail], target_loc) do
		[ [time, target_loc, temp, rain] | for_location(tail, target_loc) ]
	end

	def for_location([ _ | tail], target_loc), do: for_location(tail, target_loc)
end
```

现在，第二个函数子句匹配的条件变成有target_loc参数存在，并且把其放在在head列表中进行匹配。


我们还可以优化。我们对head列表中的其他三个元素并不感兴趣，只需要location。因此可以在head的列表中忽略不需要的元素。幸运的是Elixir模式匹配也是可以递归，我们可以在模式中匹配模式：


lists/weather3.exs

```elixir
defmodule WeatherHistory do

	def for_location([], target_loc), do: []

	def for_location([ head = [_, target_loc, _, _ ] | tail], target_loc) do

		[ head | for_location(tail, target_loc) ]

	end

	def for_location([ _ | tail], target_loc), do: for_location(tail, target_loc)
end
```



关键的改变在这行：

```elxiir
def for_location([ head = [_, target_loc, _, _ ] | tail], target_loc)
```


与前一个版本比较：

```elixir
def for_location([ [ time, target_loc, temp, rain ] | tail], target_loc)
```

在新的版本中，我们使用占位符忽略了我们不关心的值。但是我们使用了一个四个元素的条目与参数head进行匹配。其含义为“通过分隔符取出头部，匹配列表的第二个元素为target_loc相同的时候，然后再把匹配的头部和变量head进行匹配。”现在已经取出了单独的子列表来作为一个条目进行匹配。


在for_location函数的体内，使用独立的字段进行匹配，生成了结果列表。

In the original body of for_location, we generated our result list using the individual fields:

```elixir
def for_location([ [ time, target_loc, temp, rain ] | tail], target_loc)

	[ [ time, target_loc, temp, rain ] | for_location(tail, target_loc) ]

end
```


在新的版本中，值使用了匹配的head变量，整个过程变得更清晰：


```elixir
def for_location([ head = [_, target_loc, _, _ ] | tail], target_loc) do
	
	[ head | for_location(tail, target_loc) ]

end
```





### List 模块实战 


The List module provides a set of functions that operate on lists.

List模块提供了一些操作列表的函数。


#### 列表联合

```elixir
iex> [1,2,3] ++ [4,5,6]
[1, 2, 3, 4, 5, 6]
```


#### 抹平（Flatten）


```elixir
iex> List.flatten([[[1], 2], [[[3]]]])
[1, 2, 3]
```


#### Folding(类似reduce，但是可以选择方向)

```elixir
iex> List.foldl([1,2,3], "", fn value, acc -> "#{value}(#{acc})" end)
"3(2(1()))"
iex> List.foldr([1,2,3], "", fn value, acc -> "#{value}(#{acc})" end)
"1(2(3()))"
```


#### 归并(Merging)和拆分(splitting)

```elixir
iex> l = List.zip([[1,2,3], [:a,:b,:c], ["cat", "dog"]])
[{1, :a, "cat"}, {2, :b, "dog"}]
iex> List.unzip(l)
[[1, 2], [:a, :b], ["cat", "dog"]]
```


#### 列表中访问元组

```elixir
iex> kw = [{:name, "Dave"}, {:likes, "Programming"}, {:where, "Dallas", "TX"}]
[{:name, "Dave"}, {:likes, "Programming"}, {:where, "Dallas", "TX"}]
iex> List.keyfind(kw, "Dallas", 1)
{:where, "Dallas", "TX"}
iex> List.keyfind(kw, "TX", 2)
{:where, "Dallas", "TX"}
iex> List.keyfind(kw, "TX", 1)
nil
iex> List.keyfind(kw, "TX", 1, "No city called TX")
"No city called TX"
iex> kw = List.keydelete(kw, "TX", 2)
[name: "Dave", likes: "Programming"]
iex> kw = List.keyreplace(kw, :name, 0, {:first_name, "Dave"})
[first_name: "Dave", likes: "Programming"]
```


### 使用列表更友好

Get Friendly with Lists


当你需要操作数据流的时候，列表是一个很自然的数据结构。你可以用来解析数据，处理容器的值，存储一些函数调用的结果。非常值得花时间学习，学会他们编程将会很舒适。

下一章，我们将会见识更多的字典类型，包括图。那些数据结构让我们可以使用键值对容器来组织存储数据。
