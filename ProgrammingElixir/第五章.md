## 第五章 匿名函数 （Anonymous Functions）


本章知识：

* 匿名函数 
* 模式匹配和参数
* 高级函数
* 闭包
* 使用`&`定义匿名函数


Elixir是函数式编程语言，因此函数是一种基本类型。

匿名函数使用 `fn`关键字定义：

```
fn
parameter-list -> body
parameter-list -> body ...
end
```
`fn…end`包裹的函数体，有一点类似双引号包裹的字符串，只不过把函数当成值返回，而不是返回字符串。匿名函数可以当成参数传给其他函数，也可以传给它参数调用。


匿名函数可以有参数列表和函数体，它们使用`->`分隔。

例如：下面定义了一个匿名函数，并绑定给了一个`sum`变量，然后调用匿名函数:

```elixir
iex> sum = fn (a, b) -> a + b end
#Function<12.17052888 in :erl_eval.expr/5>
iex> sum.(1, 2)
3
```

代码的第一行创建了一个包含两个参数（a和b）匿名函数。接下来是一个`->`箭头符号，然后是函数体，这里的功能仅仅是将两个参数求和返回，接着使用`end`关键字结束函数，最后把匿名函数绑定给sum变量。

第二行代码，我们使用了sum.(1, 2)的语法进行匿名函数调用。使用`.`操作符接连括号的参数进行函数调用。（你也许会注意到，当使用命名函数的时候，不像匿名函数那样需要使用`.`）。

如果你的匿名函数没有参数，任然需要使用`.`和圆括号进行调用：

```elixir
iex> greet = fn -> IO.puts "Hello" end
#Function<12.17052888 in :erl_eval.expr/5>
iex> greet.()
Hello
:ok
```

定义函数的时候，参数列表的括号可以省略：

```elixir
iex> f1 = fn a, b -> a * b end
#Function<12.17052888 in :erl_eval.expr/5>
iex> f1.(5,6)
30
iex> f2 = fn -> 99 end
#Function<12.17052888 in :erl_eval.expr/5>
iex> f2.()
99
```

###  函数和模式匹配

当我们调用`sum.(2, 3)`，会想当然的认为把2赋值给a，把3赋值给b。但是，需要提醒一下。Elixir中不存在**赋值**。实际上这是模式匹配。


如果我输入 `a = 2`。

然后Elixir使用模式匹配，最后把2绑定给变量a。然后，当我们调用sum函数的时候会发生什么呢？我们把2和3当成参数传递给函数。Elixir尝试用参数模式a和b去匹配参数的值2和3。就像下面的情况：

```elixir
{a, b} = {1, 2}
```

举一反三，我们会想到，当调用函数的时候，甚至可以使用更复杂的模式匹配。下面的匿名函数接受一个元组参数，然后把元组的元素对调位置返回。


```elixir
iex> swap = fn { a, b } -> { b, a } end
#Function<12.17052888 in :erl_eval.expr/5>
iex> swap.( { 6, 8 } )
{8, 6}
```

下一章，我们将会介绍函数参数进行模式匹配的强大能力。

### 一个函数，多函数体 

定义一个函数的时候，可以定义不同的函数体。通过不同参数的类型和参数内容（你不能基于函数参数的个数来定义字句）

简而言之，我们通过参数的模式匹配选择函数字子句进行调用。下面的例子中，我们知道打开文件返回一个元组。成功打开的时候返回元组的第一个元素是`:ok`。打开失败则返回第二个`error`因此，我们可以写一个函数用来显示文件是否正确打开。

```elixir
iex> handle_open = fn
 ...> {:ok, file} -> "Read data: #{IO.read(file, :line)}"
 ...> {_, error} -> "Error: #{:file.format_error(error)}"
 ...> end
 #Function<12.17052888 in :erl_eval.expr/5>
 iex> handle_open.(File.open("code/intro/hello.exs")) # this file exists
 "Read data: IO.puts \"Hello, World!\"\n"
 iex> handle_open.(File.open("nonexistent")) # this one doesn't
 "Error: no such file or directory"
```
仔细看一下函数的定义。第二和第三行定义了两个不同的函数体。每一个函数体都接受一个单独的元组。第一个函数体的参数元组第一个元素是`:ok`.而另外一个函数体则是`_`。这两个元组正好可以匹配文件打开成功或者失败的返回值。

第六行我们将`File.open`打开存在的文件，并把结果作为参数调用`handle_open`函数。这里函数将会传入一个元组`{:ok,file}`，这个参数正好喝第二行的函数子句模式匹配，匹配的代码中`IO.read`将会读出文件内容。

再一次调用`handle_open `。这一串我们尝试读取一个不存在的文件。此时读取文件的函数返回元组是(`{:error,:enoent}`) 。这个参数和函数的第二个字句匹配。因此会执行第二个函数字句的内容。


注意上面那段代码。第三行我们调用`:file.format_error`。`:file`是一个Erlang文件模块的引用，也就是调用了erlang模块的`format_error`函数。这与第六行的`File.open`调用不一样。后者使用了Elixir内建的模块。该例子正好展示了在Elixir环境中与Erlang的无缝对接。你可以在Elixir中使用任何Erlang的库。Erlang中又很丰富的库足够你选择。当然需要注意，毕竟两个语言的调用方式不一样。

例子的最后展示了elixir的字符串插入操作。`#{变量}`定义了字符串格式化占位方式。这里面的占位变量会被真实的字符串替换。

### 更多大型例子

`handle_open`有好几行，在iex输入这么多字符很不便。一旦某一行输入错误，我们不得不重新输入整个函数。

比较好的替代方案是，将这些代码写入一个文件中。然后打开iex，通过iex运行文件。

first_steps/handle_open.exs

```elixir
handle_open = fn
{:ok, file} -> "First line: #{IO.read(file, :line)}"
{_, error} -> "Error: #{:file.format_error(error)}"
end

IO.puts handle_open.(File.open("Rakefile")) # call with a file that exists

IO.puts handle_open.(File.open("nonexistent")) # and then with one that doesn't
```

打开iex，输入:

```elixir
c "handle_open.exs"
```

编译执行我们写的源文件

不用iex，可以使用elixir命令行工具

```shell
$ elixir handle_open.exs
```

我们使用`.exs`文件扩展名。前面已经介绍过，这样方便我们直接运行源码文件（就像脚本一样）。当需要编译源码文件的时候，可以改为`.ex`扩展名。


### 函数可以返回函数 


奇怪的代码：

```elixir
iex> fun1 = fn -> fn -> "Hello" end end
#Function<12.17052888 in :erl_eval.expr/5>
iex> fun1.()
#Function<12.17052888 in :erl_eval.expr/5>
iex> fun1.().()
"Hello"
```


第一行就很奇怪，有一点难读懂，因为没有很好的格式化：

```elixir
fun1 = fn ->
  fn ->
    "Hello"
  end
end
```
变量`fun1`绑定了一个函数。这个函数没有参数，函数体又定义了一个你们函数，内函也没有参数，但是返回一个字符串`“Hello”`。

当我们使用`fun1.()`调用外函数的时候，返回内函数。然后使用`fun1.().()`调用内函数结果。

写`fun1.().()`看起来不正常。但是我们可以调用外函数再绑定一个新的变量。再通过新的变量去调用内函数：

```
iex> fun1 = fn -> (fn -> "Hello" end) end
#Function<12.17052888 in :erl_eval.expr/5>
iex> other = fun1.()
#Function<12.17052888 in :erl_eval.expr/5>
iex> other.()
"Hello"
```

### 函数存储他的原始环境


多了解函数的特性：

```elixir
iex> greeter = fn name -> (fn -> "Hello #{name}" end) end
#Function<12.17052888 in :erl_eval.expr/5>
iex> dave_greeter = greeter.("Dave")
#Function<12.17052888 in :erl_eval.expr/5>
iex> dave_greeter.()
"Hello Dave"
```

上面定义了带有`name`参数的外函数。与所有参数一样，函数体`name`都是可用的。内函数把`name`与格式化字符串返回。


当我们调用外函数的时候，返回内函数。此时，还没有把`name`传递到内涵上中。但是当我再次嗲用内涵上的时候（dave_greeter.()）。此时`name`将会替换内函数的格式化占位符。

但是有一些奇怪的事情发生。内函数使用了外还是的`name`参数。在`greeter.("Dave")`调用的时候返回了，此时外函数其实已经执行完毕了，参数应该离开其作用域了。但是，当我们返回内涵上的时候，实际上还是可以愉快的使用外函数的参数。

这样是可行的，因为Elixir的函数在定义的时候，就会自动的与作用域的变量进行绑定。例子中，当我们的定义了内函数，它继承了外函数作用域的变量绑定。这就是闭包作用域绑定了变量，然后保持这个状态知道被使用。


继续玩玩更多的东西。

#### 参数化函数

上一个例子。外函数接受一个参数，而内函数没有参数。让我们尝试一下不同的例子，即内函数也有参数的情况：


```elixir
iex> add_n = fn n -> (fn other -> n + other end) end
#Function<12.17052888 in :erl_eval.expr/5>
iex> add_two = add_n.(2)
#Function<12.17052888 in :erl_eval.expr/5>
iex> add_five = add_n.(5)
#Function<12.17052888 in :erl_eval.expr/5>
iex> add_two.(3)
5
iex> add_five.(7)
12
```

例子中，内函数使用外函数的变量`n`，并和其自身的参数`other`相加。每一次我们调用外函数，就传递了一个n，然后返货一个把n当成其自己参数的函数。


### 把函数当成参数传递 

函数也是值，因此我们可以把其传给别的函数。

```elixir
iex> times_2 = fn n -> n * 2 end
#Function<12.17052888 in :erl_eval.expr/5>
iex> apply = fn (fun, value) -> fun.(value) end
#Function<12.17052888 in :erl_eval.expr/5>
iex> apply.(times_2, 6)
12
```

这个例子中，apply绑定匿名函数，其中第一个参数是一个匿名函数。它返回第一个参数（匿名函数）使用第二个参数作为其自身的参数调用的结果。

传递函数将会让Elixir代码无所不能。例如，内建的Enum模块有一个map函数。这个函数接受两个参数，一个容器集合和一个参数函数。该函数将容器里的每一个元素传入参数函数进行调用，并将调用的结果返回的值放入新容器，并返回新容器。看下面的例子：

```elixir
iex> list = [1, 3, 5, 7, 9]
[1, 3, 5, 7, 9]
iex> Enum.map list, fn elem -> elem * 2 end
[2, 6, 10, 14, 18]
iex> Enum.map list, fn elem -> elem * elem end
[1, 9, 25, 49, 81]
iex> Enum.map list, fn elem -> elem > 6 end
[false, false, false, true, true]
```

### `&`符号

创建简短的函数十分普遍，因此Elixir提供了一个缩写的语法糖。下看看例子我们再继续：

```elixir
iex> add_one = &(&1 + 1) 		# 等价于 add_one = fn (n) -> n + 1 end
#Function<6.17052888 in :erl_eval.expr/5>
iex> add_one.(44)
45
iex> square = &(&1 * &1)
#Function<6.17052888 in :erl_eval.expr/5>
iex> square.(8)
64
iex> speak = &(IO.puts(&1))
&IO.puts/1
iex> speak.("Hello")
Hello
:ok
```


`&`可以将变大时转换成函数。在表达式中，`&1`, `&2`是函数参数占位符，分别表示第一个和第二个参数。`&(&1 + &2)` 可以把表达式 `p1 + p2` 转换成 `fn p1, p2 -> p1 + p2 end`


Elixir很智能。仔细看前一个例子`speak`。Elixir生成了一个匿名函数，也就是 `&(IO.puts(&1))`将会转换成 `fn x -> IO.puts(x) end`。但是`Elixir`可以识别函数体重有一个命名函数---`IO`模块的`puts`函数，其中匿名函数的第一个参数也正是这个函数的参数。`Elixir`可以使用`&`符号做为函数`IO.puts/1`的引用来作进一步优化。

如下的例子，函数的参数必须填写正确的顺序：

```elixir
iex> rnd = &(Float.round(&1, &2))
&Float.round/2
iex> rnd.(5.673, 2)
5.67
iex> rnd = &(Float.round(&2, &1))
#Function<12.17052888 in :erl_eval.expr/5>
iex> rnd.(2, 5.673)
5.67
```

你可能注意到了定义的函数的时候弹出了引用`Erlang`的信息。因为`Elixir`是运行在Erlang虚拟机上的。如果你使用`&abs(&1)`会看到更多的证据。`Elixir`的引用Erlang库的绝对值`abs`函数，只需要写`&:erlang.abs/1`。

```elixir
iex> abs -1
1
iex> abs_fn = &:erlang.abs/1
&:erlang.abs/1
iex> abs_fn.(-1)
1
```

因为`[]`和`{}`是Elixir中定义列表和元组的操作符，它们也可以转成成函数。下面的函数就是繁华了一个元组，里面有两个表达式，分别对两个参数做整除和取余操作。

```elixir
iex> divrem = &{ div(&1,&2), rem(&1,&2) }
#Function<12.17052888 in :erl_eval.expr/5>
iex> divrem.(13, 5)
{2, 3}
```

`&`还有另外一种形式。你可以用`&`和函数的定义式（函数名和参数个数的形式，function name/arity）来引用函数，会返回一个可调用的匿名函数。匿名函数的参数将会被传到这个引用函数中。

我们已经知道：当我们在iex中输入`&(IO.puts(&1))`，`puts`是`IO`模块的函数，它接收一个参数。Elixir中它的函数定义式为`IO.puts/1`。如果我们在这个表达式前加上`&`，我们将会得到这个函数的引用。可以理解为这样的组合得到了一个函数的别名。来看几个例子：


```elixir
iex> l = &length/1   # length/1 是一个函数，使用&引用length/1 函数，得到一个length/1 函数的别名。使用别名调用该函数（匿名函数方式调用）。
&:erlang.length/1
iex> l.([1,3,5,7])
4
iex> len = &Enum.count/1
&Enum.count/1
iex> len.([1,2,3,4])
4
iex> m = &Kernel.min/2  # Erlang 函数的别名
&:erlang.min/2
iex> m.(99,88)
88
```

我们写了命名函数的定义式，就像我们定义了函数一样。（实际上并没有定义函数，而是引用了函数而已）。

`&`其实是匿名函数的简写，在`Elixir`中很有用:

```
iex> Enum.map [1,2,3,4], &(&1 + 1)
[2, 3, 4, 5]
iex> Enum.map [1,2,3,4], &(&1 * &1)
[1, 4, 9, 16]
iex> Enum.map [1,2,3,4], &(&1 < 3)
[true, true, false, false]
```

### 函数是核心

本书的开篇，我们就宣传函数编程的基础是数据转换。函数是转换的引擎，他们是Elixir的心脏。

目前为止，我们已经学习了匿名函数，并绑定给遍历。函数也是可以有名字的。

Elixir也支持命名函数（named function 以后统称命名函数为函数），下一章，我们就会学到。


