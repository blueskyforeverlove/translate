

Chapter 16





OTP: Servers




* * *



If you’ve been following Elixir or Erlang, you’ve probably come across OTP. It is often hyped as the answer to all high-availability distributed-application woes. It isn’t, but it certainly solves many problems that you’d otherwise need to solve yourself, including application discovery, failure detection and management, hot code swapping, and server structure.

First, the obligatory one-paragraph history. OTP stands for the Open Telecom Platform, but the full name is largely of historical interest and everyone just says OTP. It was initially used to build telephone exchanges and switches. But these devices have the same characteristics we want from any large online application, so OTP is now a general-purpose tool for developing and managing large systems.

OTP is actually a bundle that includes Erlang, a database (wonderfully called Mnesia), and an innumerable number of libraries. It also defines a structure for your applications. But, as with all large, complex frameworks, there is a lot to learn. In this book we’ll focus on the essentials and I’ll point you toward other information sources.

We’ve been using OTP all along—mix, the Elixir compiler, and even our issue tracker followed OTP conventions. But that use was implicit. Now we’ll make it explicit and start writing servers using OTP.





Some OTP Definitions


OTP defines systems in terms of hierarchies of applications. An application consists of one or more processes. These processes follow one of a small number of OTP conventions, called behaviors (or behaviours). There is a behavior used for general-purpose servers, one for implementing event handlers, and one for finite-state machines. Each implementation of one of these behaviors will run in its own process (and may have additional associated processes). In this chapter we’ll be implementing the server behavior, called GenServer.

A special behavior, called supervisor, monitors the health of these processes and implements strategies for restarting them if needed.

We’ll look at these components from the bottom up—this chapter will look at servers, the next will explore supervisors, and finally we’ll implement applications.





An OTP Server


When we wrote our Fibonacci server in the previous chapter,, we had to do all the message handling ourselves. It wasn’t difficult, but it was tedious. Our scheduler also had to keep track of three pieces of state information: the queue of numbers to process, the results generated so far, and the list of active PIDs.

Most servers have a similar set of needs, so OTP provides libraries that do all the low-level work for us.

When we write an OTP server, we write a module containing one or more callback functions with standard names. OTP will invoke the appropriate callback to handle a particular situation. For example, when someone sends a request to our server, OTP will call our handle_call function, passing in the request, the caller, and the current server state. Our function responds by returning a tuple containing an action to take, the return value for the request, and an updated state.





State and the Single Server


Way back when we summed the elements in a list,, we came across the idea of an accumulator, a value that was passed as a parameter when a looping function calls itself recursively.

lists/sum.exs

defmodule MyList do



def sum([], total), do: total



def sum([ head | tail ], total), do: sum(tail, head+total)



end





The parameter total maintains the state while the function trundles down the list.

In our Fibonacci code, we maintained a lot of state in the schedule_processes function. In fact, all three of its parameters were used for state information.

Now think about servers. They use recursion to loop, handling one request on each call. So they can also pass state to themselves as a parameter in this recursive call. And that’s one of the things OTP manages for us. Our handler functions get passed the current state (as their last parameter), and they return (among other things) a potentially updated state. Whatever state a function returns is the state that will be passed to the next request handler.





Our First OTP Server


Let’s write what is possibly the simplest OTP server. You pass it a number when you start it up, and that becomes the current state of the server. When you call it with a :next_number request, it returns that current state to the caller, and at the same time increments the state, ready for the next call. Basically, each time you call it you get an updated sequence number.





Create a New Project Using Mix


Start by creating a new mix project in your work directory. We’ll call it sequence.

$ mix new sequence



* creating README.md



* creating .gitignore



* creating mix.exs



* creating lib



* creating lib/sequence.ex



* creating lib/sequence



* creating test



* creating test/test_helper.exs



* creating test/sequence_test.exs





Create the Basic Sequence Server


Now we’ll create Sequence.Server, our server module. Add the file server.ex to the sequence/ directory under lib.

otp-server/1/sequence/lib/sequence/server.ex

Line 1defmodule Sequence.Server do



2 use GenServer



3



4 def handle_call(:next_number, _from, current_number) do



5 { :reply, current_number, current_number+1 }



6 end



7 end





The first thing to note is line 2. The use line effectively adds the OTP GenServer behavior to our module. This is what lets it handle all the callbacks. It also means we don’t have to define every callback in our module—the behavior defines defaults for them all.

When a client calls our server, GenServer invokes the handle_call function that follows. It receives

the information the client passed to the call as its first parameter,

the PID of the client as the second parameter,

and the server state as the third parameter.



Our implementation is simple: we return a tuple to OTP.

{ :reply, current_number, current_number+1 }





The reply element tells OTP to reply to the client, passing back the value that is the second element. Finally, the tuple’s third element defines the new state. This will be passed as the last parameter to handle_call the next time it is invoked.





Fire Up Our Server Manually


We can play with our server in iex. Open it in the project’s main directory, remembering the -S mix option.

$ iex -S mix



iex> { :ok, pid } = GenServer.start_link(Sequence.Server, 100)



{:ok,#PID<0.71.0>}



iex> GenServer.call(pid, :next_number)



100



iex> GenServer.call(pid, :next_number)



101



iex> GenServer.call(pid, :next_number)



102





We’re using two functions from the Elixir GenServer module. The start_link function behaves like the spawn_link function we used in the previous chapter. It asks GenServer to start a new process and link to us (so we’ll get notifications if it fails). We pass in the module to run as a server: the initial state (100 in this case). We could also pass GenServer options as a third parameter, but the defaults work fine here.

We get back a status (:ok) and the server’s PID. The call function takes this PID and calls the handle_call function in the server. The call’s second parameter is passed as the first argument to handle_call.

In our case, the only value we need to pass is the identity of the action we want to perform, :next_number. If you look at the definition of handle_call in the server, you’ll see that its first parameter is :next_number. When Elixir invokes the function, it pattern-matches the argument in the call with this first parameter in the function. A server can support multiple actions by implementing multiple handle_call functions with different first parameters.

If you want to pass more than one thing in the call to a server, pass a tuple. For example, our server might need a function to reset the count to a given value. We could define the handler as

def handle_call({:set_number, new_number}, _from, _current_number) do



{ :reply, new_number, new_number }



end





and call it with

iex> GenServer.call(pid, {:set_number, 999})



999





Similarly, a handler can return multiple values by packaging them into a tuple or list.

def handle_call({:factors, number}, _, _) do



{ :reply, { :factors_of, number, factors(number)}, [] }



end





Your Turn


Exercise: OTP-Servers-1

You’re going to start creating a server that implements a stack. The call that initializes your stack will pass in a list of the initial stack contents.

For now, implement only the pop interface. It’s acceptable for your server to crash if someone tries to pop from an empty stack.

For example, if initialized with [5,"cat",9], successive calls to pop will return 5, "cat", and 9.





One-Way Calls


The call function calls a server and waits for a reply. But sometimes you won’t want to wait because there is no reply coming back. In those circumstances, use the GenServer cast function. (Think of it as casting your request into the sea of servers.)

Just like call is passed to handle_call in the server, cast is sent to handle_cast. Because there’s no response possible, the handle_cast function takes only two parameters: the call argument and the current state. And because it doesn’t want to send a reply, it will return the tuple {:noreply, new_state}.

Let’s modify our sequence server to support an :increment_number function. We’ll treat this as a cast, so it simply sets the new state and returns.

otp-server/1/sequence/lib/sequence/server.ex

Line 1defmodule Sequence.Server do



- use GenServer



-



- def handle_call(:next_number, _from, current_number) do



5 { :reply, current_number, current_number+1 }



- end



-



»- def handle_cast({:increment_number, delta}, current_number) do



»- { :noreply, current_number + delta}



»10 end



- end





Notice that the cast handler takes a tuple as its first parameter. The first element is :increment_number, and is used by pattern matching to select the handlers to run. The second element of the tuple is the delta to add to our state. The function simply returns a tuple, where the state is the previous state plus this number.

To call this from our iex session, we first have to recompile our source. The r command takes a module name and recompiles the file containing that module.

iex> r Sequence.Server



.../sequence/lib/sequence/server.ex:2: redefining module Sequence.Server



{Sequence.Server,[Sequence.Server]]





Even though we’ve recompiled the code, the old version is still running. The VM doesn’t hot-swap code until you explicitly access it by module name. So, to try our new functionality we’ll create a new server. When it starts, it will pick up the latest version of the code.

iex> { :ok, pid } = GenServer.start_link(Sequence.Server, 100)



{:ok,#PID<0.60.0>}



iex> GenServer.call(pid, :next_number)



100



iex> GenServer.call(pid, :next_number)



101



iex> GenServer.cast(pid, {:increment_number, 200})



:ok



iex> GenServer.call(pid, :next_number)



302





Tracing a Server’s Execution


The third parameter to start_link is a set of options. A useful one during development is the debug trace, which logs message activity to the console.

We enable tracing using the debug option:

» iex> {:ok,pid} = GenServer.start_link(Sequence.Server, 100, [debug: [:trace]])



{:ok,#PID<0.68.0>}



iex> GenServer.call(pid, :next_number)



*DBG* <0.68.0> got call next_number from <0.25.0>



*DBG* <0.68.0> sent 100 to <0.25.0>, new state 101



100



iex> GenServer.call(pid, :next_number)



*DBG* <0.68.0> got call next_number from <0.25.0>



*DBG* <0.68.0> sent 101 to <0.25.0>, new state 102



101





See how it traces the incoming call and the response we send back. A nice touch is that it also shows the next state.

We can also include :statistics in the debug list to ask a server to keep some basic statistics:

iex> {:ok,pid} = GenServer.start_link(Sequence.Server, 100,



» ...> [debug: [:statistics]])



{:ok,#PID<0.69.0>}



iex> GenServer.call(pid, :next_number)



100



iex> GenServer.call(pid, :next_number)



101



iex> :sys.statistics pid, :get



{:ok,[start_time: {{2013,4,26},{18,17,16}}, current_time: {{2013,4,26},{18,17,28}},



reductions: 50, messages_in: 2, messages_out: 0]}





Most of the fields should be fairly obvious. Timestamps are given as {{y,m,d},{h,m,s}} tuples. And the reductions value is a measure of the amount of work the server does. It is used in process scheduling as a way of making sure all processes get a fair share of the available CPU.

The Erlang sys module is your interface to the world of system messages. These are sent in the background between processes—they’re a bit like the backchatter in a multiplayer video game. While two players are engaged in an attack (their real work), they can also be sending each other background messages: “Where are you?”, “Stop moving”, and so on.

The list associated with the debug parameter you give to GenServer is simply the names of functions to call in the sys module. If you say [debug: [:trace, :statistics]], then those functions will be called in sys, passing in the server’s PID. Look at the documentation for sys to see what’s available.[27]

This also means you can turn things on and off after you have started a server. For example, you can enable tracing on an existing server using

iex> :sys.trace pid, true



:ok



iex> GenServer.call(pid, :next_number)



*DBG* <0.69.0> got call next_number from <0.25.0>



*DBG* <0.69.0> sent 105 to <0.25.0>, new state 106



105



iex> :sys.trace pid, false



:ok



iex> GenServer.call(pid, :next_number)



106





get_status is another useful sys function:

iex> :sys.get_status pid



{:status,#PID<0.57.0>,{:module,:gen_server},[["$ancestors": [#PID<0.25.0>],



"$initial_call":



{Sequence.Server,:init,1}],:running,#PID<0.25.0>,[],



[header: 'Status for generic server <0.57.0>',



data: [{'Status',:running},{'Parent',#PID<0.25.0>},{'Logged events',[]}],



data: [{'State',102}]]]}





This is the default formatting of the status message GenServer provides. You have the option to change the 'State' part to return a more application-specific message by defining format_status. This receives an option describing why the function was called, as well as a list containing the server’s process dictionary and the current state. (Note that in the code that follows, the string State in the response is in single quotes.)

otp-server/1/sequence/lib/sequence/server.ex

def format_status(_reason, [ _pdict, state ]) do



[data: [{'State', "My current state is '#{inspect state}', and I'm happy"}]]



end





If we ask for the status in iex, we get the new message (after restarting the server):

iex> :sys.get_status pid



{:status,#PID<0.61.0>,{:module,:gen_server},[["$ancestors": [#PID<0.25.0>],



"$initial_call": {Sequence.Server,:init,1}],:running,#PID<0.25.0>,



[trace: true],[header: 'Status for generic server <0.61.0>',



{'Parent',#PID<0.25.0>},{'Logged events',[]}],



data: [{'State',"My current state is '103', and I'm happy"}]]]}





Your Turn


Exercise: OTP-Servers-2

Extend your stack server with a push interface that adds a single value to the top of the stack. This will be implemented as a cast.

Experiment in iex with pushing and popping values.





GenServer Callbacks


GenServer is an OTP protocol. OTP works by assuming that your module defines a number of callback functions (six, in the case of a GenServer). If you were writing a GenServer in Erlang, your code would have to contain implementations of all six.

When you add the line use GenServer to a module, Elixir creates default implementations of these six callback functions. All we have to do is override the ones where we add our own application-specific behaviour. Our examples so far have used the two callbacks handle_call and handle_cast. Here’s a full list:

init(start_arguments)

Called by GenServer when starting a new server. The parameter is the second argument passed to start_link. Should return {:ok, state} on success, or {:stop, reason} if the server could not be started.



You can specify an optional timeout using {:ok, state, timeout}, in which case GenServer sends the process a :timeout message whenever no message is received in a span of timeout ms. (The message is passed to the handle_info function.)



The default GenServer implementation sets the server state to the argument you pass.



handle_call(request, from, state)

Invoked when a client uses GenServer.call(pid, request). The from parameter is a tuple containing the PID of the client and a unique tag. The state parameter is the server state.



On success returns {:reply, result, new_state}. The list that follows this one,, shows other valid responses.



The default implementation stops the server with a :bad_call error, so you’ll need to implement handle_call for every call request type your server implements.



handle_cast(request, state)

Called in response to GenServer.cast(pid, request).



A successful response is {:noreply, new_state}. Can also return {:stop, reason, new_state}.



The default implementation stops the server with a :bad_cast error.



handle_info(info, state)

Called to handle incoming messages that are not call or cast requests. For example, timeout messages are handled here. So are termination messages from any linked processes. In addition, messages sent to the PID using send (so they bypass GenServer) will be routed to this function.



terminate(reason, state)

Called when the server is about to be terminated. However, as we’ll discuss in the next chapter, once we add supervision to our servers, we don’t have to worry about this.



code_change(from_version, state, extra)

OTP lets us replace a running server without stopping the system. However, the new version of the server may represent its state differently from the old version. The code_change callback is invoked to change from the old state format to the new.



format_status(reason, [pdict, state])

Used to customize the state display of the server. The conventional response is [data: [{'State', state_info}]].



The call and cast handlers return standardized responses. Some of these responses can contain an optional :hibernate or timeout parameter. If hibernate is returned, the server state is removed from memory but is recovered on the next request. This saves memory at the expense of some CPU. The timeout option can be the atom :infinite (which is the default) or a number. If the latter, a :timeout message is sent if the server is idle for the specified number of milliseconds.

The first two responses are common between call and cast.

{ :noreply, new_state [ , :hibernate | timeout ] }

{ :stop, reason, new_state }

Signal that the server is to terminate.



Only handle_call can use the last two.

{ :reply, response, new_state [ , :hibernate | timeout ] }

Send response to the client.



{ :stop, reason, reply, new_state }

Send the response and signal that the server is to terminate.





Naming a Process


The idea of referencing processes by their PID gets old quickly. Fortunately, there are a number of alternatives.

The simplest is local naming. We assign a name that is unique for all OTP processes on our server, and then we use that name instead of the PID whenever we reference it. To create a locally named process, we use the name: option when we start the server:

» iex> { :ok, pid } = GenServer.start_link(Sequence.Server, 100, name: :seq)



{:ok,#PID<0.58.0>}



iex> GenServer.call(:seq, :next_number)



100



iex> GenServer.call(:seq, :next_number)



101



iex> :sys.get_status :seq



{:status, #PID<0.69.0>, {:module, :gen_server},



[["$ancestors": [#PID<0.58.0>],



"$initial_call": {Sequence.Server, :init, 1}],



:running, #PID<0.58.0>, [],



[header: 'Status for generic server seq',



data: [{'Status', :running},



{'Parent', #PID<0.58.0>},



{'Logged events', []}],



data: [{'State', "My current state is '102', and I'm happy"}]]]}





Tidying Up the Interface


As we left it, our server works but is ugly to use. Our callers have to make explicit GenServer calls, and they have to know the registered name for our server process. We can do better. Let’s wrap this interface in a set of three functions in our server module: start_link, next_number, and increment_number. The first of these calls the GenServer start_link method. As we’ll cover in a couple of chapters when we look at supervisors, the name start_link is a convention. start_link must return the correct status values to OTP; as our code simply delegates to the GenServer module, this is taken care of.

Following the definition of start_link, the next two functions are the external API to issue call and cast requests to the running server process.

We’ll also use the name of the module as our server’s registered local name (hence the name: __MODULE__ when we start it, and the __MODULE__ parameter when we use call or cast).

otp-server/2/sequence/lib/sequence/server.ex

defmodule Sequence.Server do



use GenServer





#####



# External API





» def start_link(current_number) do



GenServer.start_link(__MODULE__, current_number, name: __MODULE__)



end





» def next_number do



GenServer.call __MODULE__, :next_number



end





» def increment_number(delta) do



GenServer.cast __MODULE__, {:increment_number, delta}



end





#####



# GenServer implementation





def handle_call(:next_number, _from, current_number) do



{ :reply, current_number, current_number+1 }



end





def handle_cast({:increment_number, delta}, current_number) do



{ :noreply, current_number + delta}



end





def format_status(_reason, [ _pdict, state ]) do



[data: [{'State', "My current state is '#{inspect state}', and I'm happy"}]]



end



end





When we run this code in iex, it’s a lot cleaner:

$ iex -S mix



iex> Sequence.Server.start_link 123



{:ok,#PID<0.57.0>}



iex> Sequence.Server.next_number



123



iex> Sequence.Server.next_number



124



iex> Sequence.Server.increment_number 100



:ok



iex> Sequence.Server.next_number



225





This is the pattern you should use in your servers.





Your Turn


Exercise: OTP-Servers-3

Give your stack server process a name, and make sure it is accessible by that name in iex.



Exercise: OTP-Servers-4

Add the API to your stack module (the functions that wrap the GenServer calls).



Exercise: OTP-Servers-5

Implement the terminate callback in your stack handler. Use IO.puts to report the arguments it receives.

Try various ways of terminating your server. For example, popping an empty stack will raise an exception. You might add code that calls System.halt(n) if the push handler receives a number less than 10. (This will let you generate different return codes.) Use your imagination to try different scenarios.





An OTP GenServer is just a regular Elixir process in which the message handling has been abstracted out. The GenServer behavior defines a message loop internally and maintains a state variable. That message loop then calls out to various functions that we define in our server module: handle_call, handle_cast, and so on.

We also saw that GenServer provides fairly detailed tracing of the messages received and responses sent by our server modules.

Finally, we wrapped our message-based API in module functions, which gives our users a cleaner interface and decouples them from our implementation.

But we still have an issue if our server crashes. We’ll deal with this in the next chapter, when we look at supervisors.





Footnotes


[27]

http://www.erlang.org/documentation/doc-5.8.3/lib/stdlib-1.17.3/doc/html/sys.html

