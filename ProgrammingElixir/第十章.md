## 第十章 容器处理模块 Enum 和 Stream


本章内容：

* 枚举（Enum） 模块
* 流（Stream） 模块
* 收集（Collectable） 协议
* 列表解析（Comprehensions）


Elixir中又一些类型是容器类型。我们已经见过列表和字典。此外，还有范围（range），文件（file）、字典甚至是函数。当讨论协议的时候，将会学习如何自定义容器类型。

容器之间的实现各有差别。但是他们也有共同的一面：可以迭代。一些额外的功能：可以被扩展。

从技术角度说，类型能够被迭代的原理是因为它们实现了可枚举（Enumerable）的协议

Elixir提供了两个模块，包含了一些用于迭代的函数。Eunm模块作用于容器。你会经常使用。强烈建议学习。

Stream模块可以针对容器进行惰性（lazily）求值。这意味着，只有下一个元素被需要的时候才会计算。你不回一直使用他们，可是一点需要他们，必定能为你节省时间享受生活。

我不想把本书变成充满API文档的介绍手册。你可以在线阅读文档。我会通过阅读文档根据需呀插入内容，进行介绍这两个模块（记住，Elixir的强大一方面体现在他强大的库函数中）。


### Enum 处理容器

Enum几乎是Elixir中最常用的库。迭代，过滤，拼合，拆分等等容器炒作，都离不开它。下面是一些常用的介绍：

* 把容器集合转换成列表：

```elixir
iex> list = Enum.to_list 1..5
[1, 2, 3, 4, 5]
```
* 拼合两个容器
```elixir
iex> Enum.concat([1,2,3], [4,5,6])
[1, 2, 3, 4, 5, 6]
iex> Enum.concat [1,2,3], 'abc'
[1, 2, 3, 97, 98, 99]
```

* 使用函数创建容器
```elixir
iex> Enum.map(list, &(&1 * 10))
[10, 20, 30, 40, 50]
iex> Enum.map(list, &String.duplicate("*", &1))
["*", "**", "***", "****", "*****"]
```

* 通过位置索引选择元素

```elixir
iex> Enum.at(10..20, 3)
13
iex> Enum.at(10..20, 20)
nil
iex> Enum.at(10..20, 20, :no_one_here)
:no_one_here
```

* 过滤：
```elixir
iex> Enum.filter(list, &(&1 > 2))
[3, 4, 5]
iex> Enum.filter(list, &Integer.is_even/1)
[2, 4]
iex> Enum.reject(list, &Integer.is_even/1)
[1, 3, 5]
```

* 排序

```elixir
iex> Enum.sort ["there", "was", "a", "crooked", "man"]
["a", "crooked", "man", "there", "was"]
iex> Enum.sort ["there", "was", "a", "crooked", "man"],
... &(String.length(&1) <= String.length(&2))
["a", "man", "was", "there", "crooked"]
iex(4)> Enum.max ["there", "was", "a", "crooked", "man"]
"was"
iex(5)> Enum.max_by ["there", "was", "a", "crooked", "man"], &String.length/1
"crooked"
```

* 拆分

```elixir
[1, 2, 3]
iex> Enum.take_every list, 2
[1, 3, 5]
iex> Enum.take_while(list, &(&1 < 4))
[1, 2, 3]
iex> Enum.split(list, 3)
{[1, 2, 3], [4, 5]}
iex> Enum.split_while(list, &(&1 < 4))
{[1, 2, 3], [4, 5]}
```

* 连接

```elixir
iex> Enum.join(list)
"12345"
iex> Enum.join(list, ", ")
"1, 2, 3, 4, 5"
```

* 判断

```elixir
iex> Enum.all?(list, &(&1 < 4))
false
iex> Enum.any?(list, &(&1 < 4))
true
iex> Enum.member?(list, 4)
true
iex> Enum.empty?(list)
false
```

* 分组归并

```elixir

iex> Enum.zip(list, [:a, :b, :c])
[{1, :a}, {2, :b}, {3, :c}]
iex> Enum.with_index(["once", "upon", "a", "time"])
[{"once", 0}, {"upon", 1}, {"a", 2}, {"time", 3}]
```

% reduce

```elixir

iex> Enum.reduce(1..100, &(&1+&2))
5050
iex> Enum.reduce(["now", "is", "the", "time"],fn word, longest ->
...> 	if String.length(word) > String.length(longest) do
...> 		word
...> 	else
...> 		longest
...> 	end
...> end)
"time"
iex> Enum.reduce(["now", "is", "the", "time"], 0, fn word, longest ->
...> 	if String.length(word) > longest do
...> 		String.length(word)
...> 	else
...> 		longest
...> 	end
...> end)
4
```

* 组合
```elixir
iex> import Enum
iex> deck = for rank <- '23456789TJQKA', suit <- 'CDHS', do: [suit,rank]
['C2', 'D2', 'H2', 'S2', 'C3', 'D3', ... ]
iex> deck |> shuffle |> take(13)
['DQ', 'S6', 'HJ', 'H4', 'C7', 'D6', 'SJ', 'S9', 'D7', 'HA', 'S4', 'C2', 'CT']
iex> hands = deck |> shuffle |> chunk(13)
[['D8', 'CQ', 'H2', 'H3', 'HK', 'H9', 'DK', 'S9', 'CT', 'ST', 'SK', 'D2', 'HA'],
['C5', 'S3', 'CK', 'HQ', 'D3', 'D4', 'CA', 'C8', 'S6', 'DQ', 'H5', 'S2', 'C4'],
['C7', 'C6', 'C2', 'D6', 'D7', 'SA', 'SQ', 'H8', 'DT', 'C3', 'H7', 'DA', 'HT'],
['S5', 'S4', 'C9', 'S8', 'D5', 'H4', 'S7', 'SJ', 'HJ', 'D9', 'DJ', 'CJ', 'H6']]
```

### 排序

排序使用sort函数

```elixir
Enum.sort(["once", "upon", "a", "time"],
&(String.length(&1) <= String.length(&2))
```

使用`<=`和`<`在排序中很有用。

### 流（Streams）-- 惰性迭代

Elixir的Enum模块是贪婪模式，传递给它一个容器的时候，它会把所有内容都载入内存。解释说会结果会是一个新的容器。例如：

enum/pipeline.exs

```elixir
[ 1, 2, 3, 4, 5 ]
|> Enum.map(&(&1*&1))
|> Enum.with_index
|> Enum.map(fn {value, index} -> value - index end)
|> IO.inspect #=> [1,3,7,13,21]
```


第一个`map`函数接受一个列表参数，并返回一个元素开方的新列表。`with_index`函数列表的索引元组。第二个map函数通过你们函数处理索引元组，最后生成的数据传给`IO.inspect`。

因此，管道的每一次操作输出结构的时候都生成了一次列表。

看看不同的处理方式，通过读一个文件，找出文件中最长的单词。


enum/longest_line.exs
```elixir
IO.puts File.read!("/usr/share/dict/words")
|> String.split
|> Enum.max_by(&String.length/1)
```

这种情况下，读文件的时候会把整个字典载入到内存（我的机器占用了2.4MB），然后在找出最长的单纯的时候需要拆分成字符列表（大概236,000个元素）

在这些例子中，我们调用代码，就会使用Enum模块，每一个从调用就载入一个容器，返回一个新的容器。

我们预期的方案是需要处理容器的元素才载入元素。无需最开始就把整个容器都载入内存。只需要把当前的元素从一个函数传入另外一个函数处理即可。是时候让`Streams`模块闪亮登场了。



### 流（Stream）是可组合的枚举器（Enumerator）

创建一个流：

```elixir
iex> s = Stream.map [1, 3, 5, 7], &(&1 + 1)
#Stream<[enum: [1, 3, 5, 7], funs: [#Function<37.75994740/1 in Stream.map/2>] ]>
```

如果调用`Enum.map`，会马上看见输出的结果`[2,4,6,8]`。调用`Stream.map`得到了一个流类型，可以看到原始的两个参数。

如何从流中开始计算真正的结果呢？只要把流看成是一个容器，然后传入`Enum`模块的函数处理就行了：


```elixir
iex> s = Stream.map [1, 3, 5, 7], &(&1 + 1)
#Stream<...>
iex> Enum.to_list s
[2, 4, 6, 8]
```

流也是可枚举的，同样也可以把流传给另外一个流的函数。因此，这种特性称之为可组装(`composable`)

```elixir
iex> squares = Stream.map [1, 2, 3, 4], &(&1*&1)
#Stream<[enum: [1, 2, 3, 4],
funs: [#Function<32.133702391 in Stream.map/2>] ]>
iex> plus_ones = Stream.map squares, &(&1+1)
#Stream<[enum: [1, 2, 3, 4],
funs: [#Function<32.133702391 in Stream.map/2>,
#Function<32.133702391 in Stream.map/2>] ]>
iex> odds = Stream.filter plus_ones, fn x -> rem(x,2) == 1 end
#Stream<[enum: [1, 2, 3, 4],
funs: [#Function<26.133702391 in Stream.filter/2>,
#Function<32.133702391 in Stream.map/2>,
#Function<32.133702391 in Stream.map/2>] ]>
iex> Enum.to_list odds
[5, 17]
```

当然，这样写：

enum/stream1.exs

```elixir
[1,2,3,4]
|> Stream.map(&(&1*&1))
|> Stream.map(&(&1+1))
|> Stream.filter(fn x -> rem(x,2) == 1 end)
|> Enum.to_list
```

注意了，我们并没有创建中间状态的列表，只是把容器的每一个元素都传入函数链中。前一个`iex`的会话给出了工作流的传递的函数链提示，可以看到流的元素被轮流的处理。

流不仅仅用来处理列表。`Elixir`的很多模块都支持流。例如，使用流实现我们找出**最大单词**：


enum/stream2.exs
```elixir
IO.puts File.open!("/usr/share/dict/words")
|> IO.stream(:line)
|> Enum.max_by(&String.length/1)
```

这里有一个神奇的方法`IO.stream`，他可以把IO设备的每一行数据转换成流。这个概念很有用，并且还可以简写：

enum/stream3.exs

```elixir
IO.puts File.stream!("/usr/share/dict/words") |> Enum.max_by(&String.length/1)
```
这样出来的的好处就是没有中间态的存储。不好的一面就是需要运行两次，比前一个例子稍慢。可是，考虑到从远程服务器或者其他传感器读取数据。连续的数据不会马上到来，甚至可能会持续到来。使用`Enum`实现的例子，我们不得不等待所有远程数据都到来才能处理。使用流则无需等待，数据以来就可以处理。可以来多少，处理多少。

### 无限流

流是惰性的，事情不需要准备所有容器的元素。例如：


```elixir
iex> Enum.map(1..10_000_000, &(&1+1)) |> Enum.take(5)
[2, 3, 4, 5, 6]
```
需要运行8秒才能看到结果。`Elixir`先创建一个包含1000万个元素的列表，然后再处理前五个元素。如果换成流的版本：


```elixir
iex> Stream.map(1..10_000_000, &(&1+1)) |> Enum.take(5)
[2, 3, 4, 5, 6]
```

结果马上就返回了。因为后续处理只需要从流获取5个元素。一旦流产生了数据，马上就会被处理，没有延迟，处理完5个元素之后就返回了，没有多余的处理过程。

这些例子中流都是，流是是由边界的，但它同样可以很好永远持续，但是不要那么做。我们需要基于函数创建流。


### 自定义Streams


流是Elixir的库实现的。并没有支持特别的运行时状态。可是，这并不意味着放弃非常底层的方法创建自定义可流化的类型。真实的实现很复杂（同样的，字符串理论也很复杂）。实际上，可以使用一些有用的函数来完成艰巨的任务。流提供了很多函数，包括`cycle`, `repeatedly`, `iterate`, `unfold`, 和 `resource`。

从最简单的开始：

* Stream.cycle

Stream.cycle接受一个可枚举的容器，返回一个无限周期的流。当枚举到最后一个元素的时候，将会又重头开始。下面的例子使用该函数生成一个HTML表单文档，表单的每一行交替使用green和white的class：

```elixir
iex> Stream.cycle(~w{ green white }) |>
...> Stream.zip(1..5) |>
...> Enum.map(fn {class, value} ->
...> ~s{<tr class="#{class}"><td>#{value}</td></tr>\n} end) |>
...> IO.puts
<tr class="green"><td>1</td></tr>
<tr class="white"><td>2</td></tr>
<tr class="green"><td>3</td></tr>
<tr class="white"><td>4</td></tr>
<tr class="green"><td>5</td></tr>
```

* Stream.repeatedly

Stream.repeatedly 接收一个函数参数，返回的流对象。每当流对象的元素被访问的时候，将会调用那个参数函数：


```elixir
iex> Stream.repeatedly(fn -> true end) |> Enum.take(3)
[true, true, true]
iex> Stream.repeatedly(&:random.uniform/0) |> Enum.take(3)
[0.7230402056221108, 0.94581636451987, 0.5014907142064751]
```

* Stream.iterate

Stream.iterate(start_value, next_fun) 生成一个无限的流。第一个参数是起始值，第二个参数是一个函数，它使用第一个参数作为自己的参数来生成下一个值，并把下一个值传给iterate函数。整个流函数很常用。

例子：

```elixir
iex> Stream.iterate(0, &(&1+1)) |> Enum.take(5)
[0, 1, 2, 3, 4]
iex> Stream.iterate(2, &(&1*&1)) |> Enum.take(5)
[2, 4, 16, 256, 65536]
iex> Stream.iterate([], &[&1]) |> Enum.take(5)
[[], [[]], [[[]]], [[[[]]]], [[[[[]]]]]]
```

* Stream.unfold

现在可以探索更多的特性。Stream.unfold和iterate类类似，但是可以更详尽的知道输出的到流的值，已经把值传递给下一次迭代的过程。

只需提供一个初始值和函数。函数将会私用参数创建两个值，返回一个元组。第一个参数是呗流迭代之后繁华，第二个参数传递给函数进行下一次流迭代。如果函数返回nil，流操作将会终止。

听起来很抽象，但是unfold也是非常有用，通常用于初始化一个流，流的每一个元素都是上一个函数的状态。


关键在于生成的函数，通常形式是：

```elixir
fn state -> { stream_value, new_state } end
```

例如，斐波那契（Fibonacci）函数：
```elixir
iex> Stream.unfold({0,1}, fn {f1,f2} -> {f1, {f2, f1+f2}} end) |> Enum.take(15)
[0, 1, 1, 2, 3, 5, 8, 13, 21, 34, 55, 89, 144, 233, 377]
```

第一个参数是一个元组。我们看见初始的状态是` {0, 1}`。流的每一次迭代的值，都会返回一堆元组。新的返回又当成参数传递给流。如此迭代下去最终得到想要的结果。寄初始化为{f1,f2}，返回的结果为{f2,f1+f2}。


* Stream.resource

你怀疑，为什么流可以和资源文件交互。我们已经见识到如何把文件的行转换成流，但是这个过程可以自己实现么？你需要打开一个文件，当流开始创建的是时候，然后返回连续的文件行，当读完文件之后关闭文件。或者可能你想换成数据库操作。将游标当成流的值。当流数据抵达的时候，可以执行查询语句，然后把结果返回流中，最后再关闭查询。这个应用场景可以使用当读完文件之后实现。

Stream.resource构建于Stream.unfold之上，做了两个改动。

首先，展开第一个参数是要传递给迭代函数的初始值。但是，如果该值是一个资源，我们不希望打开它，除非流已经开始创建值，我们创建流之前不会发生。为了解决这个问题，需要的资源不是一个值，而是一个函数返回值。

其次，当资源的流已经完成了，我们需要释放资源。第三个产生将会做这样的功能。

库文档的一个例子：

```elixir
Stream.resource(fn -> File.open("sample") end,
	fn file -> case IO.read(file, :line) do
			line when is_binary(line) -> { [line], file } _ -> {:halt, file}
		end
	end,
	fn file -> File.close!(file) end)
```


The first function opens the file when the stream becomes active, and passes it to the second function. This reads the file, line by line, returning either a line and the file as a tuple, or a :halt tuple at the end of the file. The third function closes the file.

Let’s finish with a different kind of resource: time. We’ll implement a timer that counts down the number of seconds until the start of the next minute. It uses a stream resource to do this. The allocation function returns the number of seconds left until the next minute starts. It does this each time the stream is evaluated, so we’ll get a countdown that varies depending on when it is called.

The iteration function looks at the time left. If zero, it returns {:halt, 0}; otherwise it sleeps for a second and returns the current countdown as a string, along with the decremented counter.

In this case there’s no resource deallocation, so the third function does nothing.

Here’s the code:

enum/countdown.exs

```elixir
defmodule Countdown do

	def sleep(seconds) do
		receive do
			after seconds*1000 -> nil
		end
	end

	def say(text) do
		spawn fn -> :os.cmd('say #{text}') end
	end

	def timer do

		Stream.resource(
			fn -> # the number of seconds to the start of the next minute

			{_h,_m,s} = :erlang.time 60 - s - 1
	end,

	fn # wait for the next second, then return its countdown

	0 -> :halt, 0}

	count ->

		sleep(1)
			{ [inspect(count)], count - 1 }
		end,

			fn _ -> end # nothing to deallocate
			)
	end

end
```


(The eagle-eyed among you will have noticed a function called say in the Countdown module. This executes the shell command say, which, on OS X, speaks its argument. You could substitute espeak on Linux and ptts on Windows.)

Let’s play with the code.

```elixir
$ iex countdown.exs
iex> counter = Countdown.timer
#Function<17.133702391 in Stream.resource/3>
iex> printer = counter |> Stream.each(&IO.puts/1)
#Stream[enum: #Function<17.133702391 in Stream.resource/3>,
funs: [#Function<0.133702391 in Stream.each/2>] ]>
iex> speaker = printer |> Stream.each(&Countdown.say/1)
#Stream[enum: #Function<17.133702391 in Stream.resource/3>,
funs: [#Function<0.133702391 in Stream.each/2>,
#Function<0.133702391 in Stream.each/2>] ]>
```


So far, we’ve built a stream that creates time events, prints the countdown value, and speaks it. But there’s been no output, as we haven’t yet asked the stream for any values. Let’s do that now:

```elixir
iex> speaker |> Enum.take(5)
37 ** numbers are output once
36 ** per second. Even cooler,the
35 ** computer says
34 ** "thirty seven", "thirty six"…
33
["37", "36", "35", "34", "33"]
```

Cool—we must have started it around 22 seconds into a minute, so the countdown starts at 37. Let’s use the same stream again, a few seconds later:

```elixir
iex> speaker |> Enum.take(5)
29
28
27
26
25
["29", "28", "27", "26", "25"]
```

Wait some more seconds, and this time let it run to the top of the minute:

```elixir
iex> speaker |> Enum.to_list
6
5
4
3
2
1
["6", "5", "4", "3","2", "1"]
```

This is clearly not great code, as it fails to correct the sleep time for any delays introduced by our code. But it illustrates a very cool point. Lazy streams let you deal with resources that are asynchronous to your code, and the fact that they are initialized every time they are used means they’re effectively side-effect-free. Every time we pipe our stream to an Enum function, we get a fresh set of values, computed at that time.


### 流实战


In the same way that functional programming requires you to look at problems in a new way, streams ask you to look at iteration and collections afresh. Not every situation where you are iterating requires a stream. But consider using a stream when you want to defer processing until you need the data, and when you need to deal with large numbers of things without necessarily generating them all at once.


### 收集（Collectable）协议

枚举协议接收一个容器，迭代容器的每一个元素，获得元素。收集协议在某些方面恰恰相反，它可以创建一个通过插入元素创建一个容器。

并不是所有容器都是实现了收集协议。例如范围（Ranges），不能给它增加新的项目元素。

容器API是低级的api，使用列表解析（下一节介绍）的时候，通常使用Enum.into范围容器。例如，将范围的元素插入到空列表中：

```elixir
iex> Enum.into 1..5, []
[1, 2, 3, 4, 5]
```

如果列表不为空，则在列表后追加范围元素：

```elixir
iex> Enum.into 1..5, [100, 101 ]
[100, 101, 1, 2, 3, 4, 5]
```

流是实现了收集协议，因此Enum.into之后的代码可以惰性的复制标准输入到标准输出：

```elixir
iex> Enum.into IO.stream(:stdio, :line), IO.stream(:stdio, :line)
```

### 列表解析（Comprehensions）

当你写一些函数功能代码的时候，经常会需要对容器进行map和filter操作。为了让生活更加轻松（代码更易读），Elixir提供了这些功能的简写表达式：列表解析。

列表解析的原理相当简单：提供一个或多个容器，针对容器的每一个元素依次遍历，处理元素，返回处理后的元素组成的新容器。

列表解析的语法看上去还是很简单的：

```elixir
result = for generator or filter… [, into: value ], do: expression
```
`结果 = for 生成器 或者 过滤器 [into：value] do: 解析表达式`

讨论更多细节之前，先看一下节本的例子：

```elixir
iex> for x <- [ 1, 2, 3, 4, 5 ], do: x * x
[1, 4, 9, 16, 25]
iex> for x <- [ 1, 2, 3, 4, 5 ], x < 4, do: x * x
[1, 4, 9]
```
生成器（generator）会从容器中取出元素进行模式匹配。

```elixir
pattern <- list
```

模式匹配中的任何变量匹配了，那么剩下的解析也同样匹配。例如：`x <- [1,2,3]`指，绑定`x=1`进行`do:`的表达式运算，然后再绑定`x=2`，以此类推。如果有两个生成器，则会嵌套绑定操作。

```elixir
x <- [1
,2], y <- [5,6]
```
该生成器将会依次执行绑定：`x=1, y=5; x=1, y=6; x=2, y=5; and x=2, y=6`
在do表达式中可以得到 *x* 和 *y* 的值。

```elixir
iex> for x <- [1,2], y <- [5,6], do: x * y
[5, 6, 10, 12]
iex> for x <- [1,2], y <- [5,6], do: {x, y}
[{1, 5}, {1, 6}, {2, 5}, {2, 6}]
```

也可以在最后一个生成器中使用前一个生成器的绑定的变量:

```elixir
iex> min_maxes = [{1,4}, {2,3}, {10, 15}]
[{1, 4}, {2, 3}, {10, 15}]
iex> for {min,max} <- min_maxes, n <- min..max, do: n
[1, 2, 3, 4, 2, 3, 10, 11, 12, 13, 14, 15]
```
过滤器（filter）是一个谓词。它扮演了解析表达式的看门人角色--即条件表达式。如果条件为false，生成器将会迭代绑定下一个元素而不会执行解析表达式。

例如：下面的代码依次迭代绑定列将1到8之间的数字，然后把相互的乘积为10的倍数的两个数字组成一个元组对返回。

```elixir
iex> first8 = [ 1,2,3,4,5,6,7,8 ]
[1, 2, 3, 4, 5, 6, 7, 8]
iex> for x <- first8, y <- first8, x >= y, rem(x*y, 10)==0, do: { x, y }
[{5, 2}, {5, 4}, {6, 5}, {8, 5}]
```

这个列表解析迭代了64次，分别是 `x=1, y=1; x=1, y=2`等等。可是，第一个过滤器将会取出x大于等于y，也就意味着解析执行将会变成36次。

因为第一个生成器是模式匹配，因此实现重组数据结构。下面是一个交换关键字列表键值的列表解析：

```elixir
iex> reports = [ dallas: :hot, minneapolis: :cold, dc: :muggy, la: :smoggy ]
[dallas: :hot, minneapolis: :cold, dc: :muggy, la: :smoggy]
iex> for { city, weather } <- reports, do: { weather, city }
[hot: :dallas, cold: :minneapolis, muggy: :dc, smoggy: :la]
```

### 位数据也可以列表解析

位串(bitstring）(二进制或者字符串的扩展)是很多0和1的集合。毫无疑问它也可以列表解析。可以使用下面的语法进行解析：

> 位是这个容器集合的基本单元，类似列表元素与列表的关系

```elixir
iex> for << ch <- "hello" >>, do: ch
'hello'
iex> for << ch <- "hello" >>, do: <<ch>>
["h", "e", "l", "l", "o"]
```

生成器被`<<`和`>>`包裹，表示一个二进制。第一个例子：do解析表达式返回了每一个字符的码值，因此结果是`[104, 101, 108, 108, 111]`，这个字符列表的打印结果即位：`'hello'`。

第二个例子，解析表达式将码值转换成二进制串，因此结果变成了单个字符串的列表。

再一次重申，`<-`符号左边是一个模式，因此可以使用二进制的模式匹配。现在把字符串转换成八进制表示：


```elixir
iex> for << << b1::size(2), b2::size(3), b3::size(3) >> <- "hello" >>,
...> do: "0#{b1}#{b2}#{b3}"
["0150", "0145", "0154", "0154", "0157"]
```

### 作用域和列表解析


All variable assignments inside a comprehension are local to that comprehension—you will not affect the value of a variable in the outer scope.
```elixir
iex> name = "Dave"
"Dave"
iex> for name <- [ "cat", "dog" ], do: String.upcase(name)
["CAT", "DOG"]
iex> name
"Dave"
iex>
```

### 列表解析返回值


目前为止的例子中，列表解析都返回新列表。新列表由生成器迭代容器并且过滤之后经过解析表达式处理的元素组成。

可以使用可选表达式`into: parmeter`定制返回结果类型。into接收解析结果列表，然后转换，例如下面转换成图：


```elixir
iex> for x <- ~w{ cat dog }, into: %{}, do: { x, String.upcase(x) }
%{"cat" => "CAT", "dog" => "DOG"}
```
使用Map.new代码更清晰：

```elixir
iex> for x <- ~w{ cat dog }, into: Map.new, do: { x, String.upcase(x) }

%{"cat" => "CAT", "dog" => "DOG"}
```

结果插入到图：

```elixir
iex> for x <- ~w{ cat dog }, into: %{"ant" => "ANT"}, do: { x, String.upcase(x) }
%{"ant" => "ANT", "cat" => "CAT", "dog" => "DOG"}
```

第二十二章，协议（Protocols）-多态（Polymorphic）-函数（Functions）。将会介绍协议。协议可以定制不同类型的行为。`into:`表达式的值参数接收实现了可收集协议的类型，例如列表，二进制，函数，图，文件，哈希字典，哈希集合，IO，流。我们可以把他们传递给into：表达式：

```elixir
iex> for x <- ~w{ cat dog }, into: IO.stream(:stdio,:line), do: "<<#{x}>>\n"
<<cat>>
<<dog>>
%IO.Stream{device: :standard_io, line_or_bytes: :line, raw: false}
```

### 更快，超神

L. Peter Deutsch 曾经宣称，“人类使用迭代，神才用递归”（To iterate is human, to recurse divine.）。当我第一次使用Elixir的时候，十分认同这句话。递归函数里模式匹配的乐趣改变了我的设计。从哪以后，我意识到可能我已经入戏太深了。

在现实中，我们大部分的日常工作，是使用Elixir内建的枚举器。它们使代码更短小，更易理解，也许还更高效。

何时使用递归，何时使用枚举也是学习高效使用Elixir处理工作任务的一部分。优先推荐使用枚举。

下一章，将会学习处理Elixir（或Erlang）处理字符串。





