## 第十一章 字符串和二进制

本章内容：

* String模块和字符串（string）
* 字符列表（character lists）
* 处理字符串和其模式匹配

我们一直很开心的用着字符串却没有专门讨论，现在是时候学习字符串了。

### 字符串定义

`Elixir`有两种字符表示：单引号和双引号包裹的字符。除了最本质的不同职务，其他还是有很多共同之处的。

字符串是由UTF-8编码的字符组成的。



| 1             | 2       | 3             |      4        |
|:-------------:|:-------:|:-------------:|:-------------:|
| BEL \a (0x07) | \b      | \BS(0x08)     | DEL \d (0x7f) |
| ESC \e (0x1b) | \f      | \FF(0x0c)     | NL \n (0x0a)  |
| CR  \r (0x0d) | \s      | \SP(0x20)     | TAB \t (0x09) |
| VT  \v (0x0b) | \xhhh     



它们都可以使用`Elixir`的格式化字符串的表达式`#{...}`语法：

```elixir
iex> name = "dave"
"dave"
iex> "Hello, #{String.capitalize name}!"
"Hello, Dave!"
```

有特殊含义的字符可以用反斜线转义。字符串还至此`heredocs`。


### Heredocs

字符串可能是多行。为了说明这个现象，下面使用`IO.puts` 和`IO.write`两个函数。使用`write`来输出多行字符串，因为`puts`会自动换行。

```elixir
IO.puts "start"
IO.write "
my
string
"
IO.puts "end"
```
结果

```elixir
start

my
string
end
```

注意看多行的字符串，在打印之前，在两个输出字符串之间，`puts`函数多打印了一个空行。

`heredoc`将会修复整个问题。使用三个引号（''' 或者 """）可以去除你字符串里的缩进：

```elixir
IO.puts "start"
IO.write """
my
string
"""
IO.puts "end"
```

结果：

```elixir
start
my
string
end
```
`Heredocs`大量用于模块和函数编写文档的时候。



### 魔印（sigil）

与`Ruby`类似，`Elixir`对字符串简写的语法。使用`~r{...}`表示正则表达式。通常`~`定义的字符串字面量都可以称之为魔印（具有强大魔法的符号）。

魔印使用`~`符号开始，跟着一个大写或者小写的字母，然后界定内容，下面是一些用来做分界的可选符号：` <…>, {…}, […], (…), |…|, /…/, "…", 和 '…'`。

字母表示了魔印的类型：

| 字母   | 含义                    		                        |
|:-----:|:-----------------------------------------------------:|
| ~C 	 | 没有转义和计算格式化占位符的字符列表                        |
| ~c 	 | 有转义和计算格式化占位符的字符列表，等价于单引号引起来的字符    |
| ~R 	 | 没有有转义和计算格式化占位符的正则表达式                     |
| ~r 	 | 有转义和计算格式化占位符的正则表达式                        |
| ~S 	 | 没有转义和计算格式化占位符的字符串                          |
| ~s 	 | 有转义和计算格式化占位符的字符串，等价于双引号引起来的字符      |
| ~W 	 | 没有转义和计算格式化占位符的单词列表                        |
| ~w 	 | 有转义和计算格式化占位符的单词列表，等价于双引号引起来的字符    |


例如：

```elixir
iex> ~C[1\n2#{1+2}]   
'1\\n2\#{1+2}'			# 转义被忽略， {}占位符没有被求值计算
iex> ~c"1\n2#{1+2}"
'1\n23'					# 转义了，{}占位符被求值计算
iex> ~S[1\n2#{1+2}]
"1\\n2\#{1+2}"
iex> ~s/1\n2#{1+2}/
"1\n23"
iex> ~W[the c#{'a'}t sat on the mat]
["the", "c\#{'a'}t", "sat", "on", "the", "mat"]
iex> ~w[the c#{'a'}t sat on the mat]
["the", "cat", "sat", "on", "the", "mat"]
```

 `~W` 和 `~w`魔印最后可以使用可选的`a`, `c`或`s`修饰。分别用来修饰繁华原子，字符列表还是字符串。

```elixir
iex> ~w[the c#{'a'}t sat on the mat]a
[:the, :cat, :sat, :on, :the, :mat]
iex> ~w[the c#{'a'}t sat on the mat]c
['the', 'cat', 'sat', 'on', 'the', 'mat']
iex> ~w[the c#{'a'}t sat on the mat]s
["the", "cat", "sat", "on", "the", "mat"]
```

分隔符可以是任何非单词字符。成对的这些`(`, `[`, `{`, 或者 `<`都可以。

`Elixir`不会检查嵌套的分界符，例如`~s{a{b}`最终被识别为字符串`a{b`。

如果分界符是三引号开始的的，那么这个魔引会被当成是`heredoc`：

```elixir
iex> ~w"""
...> the
...> cat
...> sat
...> """

["the", "cat", "sat"]
```

如果想针对`heredoc`指定特别的魔印修饰（多数时候使用`~r`），可以在最后添加`i`。


```elixir
iex> ~r"""
...> hello
...> """i
~r/hello\n/i
```

魔印还有一个有趣的特性，那就是定制自己的魔印。再第三部分将会介绍。


### “string”的含义 


在我们继续学习之前，还有一些东西需要解释。大多数编程语言中，`'cat'`和`"cat"`都是字符串。到目前为止也是如此但是，`Elixir`中他们的表示含义却不尽相同。

`Elixir`中，字符串指使用双引号包裹的字符。单引号包裹的则是字符列表。

这十分重要，单引号和双引号的形式不一样，并且字符串的类库只适用于双引号的形式。

现在我们来探讨不同的细节部分。


### 单引号字串其实是字符列表。

字符列表实际上是一串整数的列表，每一项的值是对于字符的码值。因此，我们可以把字符列表当成列表来使用：

```elixir
iex> str = 'wombat'
'wombat'
iex> is_list str
true
iex> length str
6
iex> Enum.reverse str
'tabmow'
```

这有点让人疑惑，`iex`说它使列表，显示的却是字符。因为如果某个码值是可以打印的，那就iex就打印该码值对应的字符。你可以自己尝试：

```elixir
iex> [ 67, 65, 84 ]
'CAT'
```

通过下面的方法查看字符列表的原始码值：

```elixir
iex> str = 'wombat'
'wombat'
iex> :io.format "~w~n", [ str ]
[119,111,109,98,97,116]
:ok
iex> List.to_tuple str
{119, 111, 109, 98, 97, 116}
iex> str ++ [0]
[119, 111, 109, 98, 97, 116, 0]
```

`~w`借助`Erlang`虚拟机强制把所代表的字符格式化成码值列表。`~n`表示换行。


最后一个例子给字符列表的末尾追加一个空字节的列表。此时`iex`不再认为所有的码值都是可打印的了，因此就返回了字符的码值。

字符列表包含了`Erlang`中不能打印的码值，你会看到对应的列表：

```elixir
iex> '∂x/∂y'
[8706, 120, 47, 8706, 121]
```
因为字符列表也是列表，因此我们可以使用列表函数和模式匹配。

```elixir
iex> 'pole' ++ 'vault'
'polevault'
iex> 'pole' -- 'vault'
'poe'
iex> List.zip [ 'abc', '123' ]
[{97, 49}, {98, 50}, {99, 51}]
iex> [ head | tail ] = 'cat'
'cat'
iex> head
99
iex> tail
'at'
iex> [ head | tail ]
'cat'
```

为什么字符列表`'cat'`的头部是`99`而不是`c`呢？记住了，字符列表是字符的码值组成的，因此每一个列表项就是一个整数。因此头部`c`就是`c`的码值`99`。

可以使用`?`操作符获取一个字符的码值，`?c`返回`c`的码值。当从字符列表中分解进行模式匹配的时候这个技巧将会很有用。下面的模块实现了一个字符列表转换码值的功能。

strings/parse.exs

```elixir
defmodule Parse do

	def number([ ?- | tail ]), do: _number_digits(tail, 0) * -1

	def number([ ?+ | tail ]), do: _number_digits(tail, 0)

	def number(str), do: _number_digits(str, 0)

	defp _number_digits([], value), do: value

	defp _number_digits([ digit | tail ], value) when digit in '0123456789' do
		_number_digits(tail, value*10 + digit - ?0)
	end

	defp _number_digits([ non_digit | _ ], _) do
		raise "Invalid digit '#{[non_digit]}'"
	end
end
```
使用iex测试：

```elixir
iex> c("parse.exs")
[Parse]
iex> Parse.number('123')
123
iex> Parse.number('-123')
-123
iex> Parse.number('+123')
123
iex> Parse.number('+9')
9
iex> Parse.number('+a')
** (RuntimeError) Invalid digit 'a'
```

### 二进制（Binaries）


二进制类型是位的序列。使用`<<>>`定义字面量。大致如 `<< 项,… >>`，最简单项是0~255的整数。这些数字是存储在二进制中的连续字节。

```elixir
iex> b = << 1, 2, 3 >>
<<1, 2, 3>>
iex> byte_size b
3
iex> bit_size b
24
```

当然，你也可定制项的大小（使用位(`bit`)单位）。这个技巧在使用二进制格式化一些媒体文件和网络数据包的时候很有用。

```elixir
iex> b = << 1::size(2), 1::size(3) >>
<<9::size(5)>>
iex> byte_size b
1
iex> bit_size b
5
```

也可以存储整数，浮点数或者其他二进制类型。


```elixir
iex> int = << 1 >>
<<1>>
iex> float = << 2.5 :: float >>
<<64, 4, 0, 0, 0, 0, 0, 0>>
iex> mix = << int :: binary, float :: binary >>
<<1, 64, 4, 0, 0, 0, 0, 0, 0>>
```

让我们来看一个二进制位提取的一个例子。[`IEEE 754`协议](http://zh.wikipedia.org/zh/IEEE_754)规定了浮点数有一个位符号（`sign`）和11位的指数（`exponent`）已经52位的尾数（`mantissa`）。下面是一个浮点数转换成二进制的例子：


```elixir
iex> << sign::size(1), exp::size(11), mantissa::size(52) >> = << 3.14159::float >>
iex> (1 + mantissa / :math.pow(2, 52)) * :math.pow(2, exp-1023)
3.14159
```


### 双引号字符串本质是二进制（Binaries）


单引号定义的字符是字符列表，双引号表示的是用`UTF-8`编码的连续序列。显然，访问这样的形式在内存方面要更高效，但是这有两层含义。

首先，`UTF-8`编码表示一个字符并不都是使用一个字节，因此字节的长度和字符串的长度是不一样的。

```elixir
iex> dqs = "∂x/∂y"
"∂x/∂y"
iex> String.length dqs
5
iex> byte_size dqs
9
iex> String.at(dqs, 0)
"∂"
iex> String.codepoints(dqs)
["∂", "x", "/", "∂", "y"]
iex> String.split(dqs, "/")
["∂x", "∂y"]
```

其次，当你不再使用列表的时候，你需要学会二进制语法和列表语法。

### 字符串和Elixir库

Elixir的文档使用单词**字符串**（通常还是习惯使用单词**二进制**）表示双引号的字符串.

String模块提供了一些函数用于处理字符串。

* at(str, offset)

返回字符串在给定的偏移（`offset`），`offset`为负数的时候，表示从字符的末端开始计数。

```elixir
iex> String.at("∂og", 0)
"∂"
iex> String.at("∂og", -1)
"g"
```

* capitalize(str)

先字符串转换成小写，然后让首字母大写。

```elixir
iex> String.capitalize "école"
"École"
iex> String.capitalize "ÎÎÎÎÎ"
"Îîîîî"
```

* codepoints(str)

返回字符串的码值列表，拆分字符串


```elixir
iex> String.codepoints("José's ∂øg")
["J", "o", "s", "é", "'", "s", " ", "∂", "ø", "g"]
```

* downcase(str)

把字符串转成小写

```elixir
iex> String.downcase "ØRSteD"
"ørsted"
```

* duplicate(str, n)

返回字符串重复的叠加

```elixir
iex> String.duplicate "Ho! ", 3
"Ho! Ho! Ho! "
```

* ends_with?(str, suffix | [ suffixes ])

如果`str`的结尾与`suffixes`中任何一个匹配，则返回`true`

```elixir
iex> String.ends_with? "string", ["elix", "stri", "ring"]
true
```

* first(str)

返回字符串首字母

```elixir
iex> String.first "∂og"
"∂"
```

* graphemes(str)

返回字符串的字素（`graphemes`）。字素函数和码值函数不一样。
> 字素（Graphemes）则是各个语言书写的最小单位（比如英文的字母表，中文的汉字等）。

* last(str)

返回最后一个字形（字形就是指字符串看上去的样子）

```elixir
iex> String.last "∂og"
"g"
```
* length(str)

返回字符串字形的长度

```elixir
iex> String.length "∂x/∂y"
5
```

* ljust(str, new_length, padding \\ " ")


```elixir
iex> String.ljust("cat", 5)
"cat "
```


* lstrip(str)

出去字符串的左边的空格符

```elixir
iex> String.lstrip "\t\f Hello\t\n"
"Hello\t\n"
```

* lstrip(str, character)

依据给出的字符的码值，除去左边码值匹配的对应字符。

```elixir
iex> String.lstrip "!!!SALE!!!", ?!
"SALE!!!"
```

* next_codepoint(str)

切分字符串为码值。如果字符串为空返回`nil`。常用语基础的迭代。

strings/nextcodepoint.ex
```elixir
defmodule MyString do

	def each(str, func), do: _each(String.next_codepoint(str), func)

	defp _each({codepoint, rest}, func) do

		func.(codepoint)
		_each(String.next_codepoint(rest), func)

	end

	defp _each(nil, _), do: []

end

MyString.each "∂og", fn c -> IO.puts c end
```

结果:

```elixir
∂
o
g
```

* next_grapheme(str)
与 `next_codepoint`类似，不同之处在于返回的时字素。

* printable?(str)

如果字符是可打印输出的，则返回`true`。

```elixir
iex> String.printable? "José"
true
iex> String.printable? "\x{0000} a null"
false
```

* replace(str, pattern, replacement, options \\ [global: true, insert_replaced: nil])

使用正则替换字符，如果`:global`选项设置true，则针对整个字符串进行替换，否则只替换第一个匹配的字符。

如果`:insert_replaced`设置了数值，那么模式将从这个数值的偏移位置开始匹配。


```elixir
iex> String.replace "the cat on the mat", "at", "AT"
"the cAT on the mAT"
iex> String.replace "the cat on the mat", "at", "AT", global: false
"the cAT on the mat"
iex> String.replace "the cat on the mat", "at", "AT", insert_replaced: 0
"the catAT on the matAT"
iex> String.replace "the cat on the mat", "at", "AT", insert_replaced: [0,2]
"the catATat on the matATat"
```

* reverse(str)

反转字符串的

```elixir
iex> String.reverse "pupils"
"slipup"
iex> String.reverse "∑ƒ÷∂"
"∂÷ƒ∑"
```

* rjust(str, new_length, padding \\ " ")


```elixir
iex> String.rjust("cat", 5, ?>)
">>cat"
```

* rstrip(str)

移除右边的空格

```elixir
iex> String.rstrip(" line \r\n")
" line"
```

* rstrip(str, character)

与`lstrip(str, character)`功能类似，方向是从右边开始。


```elixir
iex> String.rstrip "!!!SALE!!!", ?!
"!!!SALE"
```

* slice(str, offset, len)

字符串切片，可以使用负数。

```elixir
iex> String.slice "the cat on the mat", 4, 3
"cat"
iex> String.slice "the cat on the mat", -3, 3
"mat"
```

* split(str, pattern \\ nil, options \\ [global: true])

切分字符串


```elixir
iex> String.split " the cat on the mat "
["the", "cat", "on", "the", "mat"]
iex> String.split "the cat on the mat", "t"
["", "he ca", " on ", "he ma", ""]
iex> String.split "the cat on the mat", ~r{[ae]}
["th", " c", "t on th", " m", "t"]
iex> String.split "the cat on the mat", ~r{[ae]}, parts: 2
["th", " cat on the mat"]
starts_with?(str, prefix | [ prefixes ])
```

可是使用前缀

```elixir
iex> String.starts_with? "string", ["elix", "stri", "ring"]
true
```

* strip(str)

移除字符串空格

```elixir
iex> String.strip "\t Hello \r\n"
"Hello"
```

* strip(str, character)

移除指定字符

```elixir
iex> String.strip "!!!SALE!!!", ?!
"SALE"
```

* upcase(str)

字符大写


```elixir
iex> String.upcase "José Ørstüd"
"JOSÉ ØRSTÜD"
```

* valid_character?(str)

单个字符判断

```elixir
iex> String.valid_character? "∂"
true
iex> String.valid_character? "∂og"
false
```


### 二进制和模式匹配

二进制的首要规则是“如有疑问，可指定每个字段的类型。”。可用的类型是二进制，位，位串（bitstring）， 字节，浮点，整型，utf8， utf16和utf32。你都可以修饰：


* size(n):  字段的位的大小。

* 符号（signed）或者 无符号（unsigned）：整型的字段是符号的？
* endianness: 大的，小的或者本地。

* 使用连字符分隔多个属性字段：

<< length::unsigned-integer-size(12), flags::bitstring-size(4) >> = data

可是，除非你需要使用二进制文件或者协议来格式化，大多数情况下使用的修饰后缀都是UTF-8字符串。


### 使用二进制处理字符串

处理列表的时候，可以通过模式匹配把列表拆分为头部和尾部。二进制存储的是字符串，我们也可以这么做。需要指定头部（UTF-8）喝剩余的二进制尾部。


strings/utf-iterate.ex

```elixir
defmodule Utf8 do

	def each(str, func) when is_binary(str), do: _each(str, func)

	defp _each(<< head :: utf8, tail :: binary >>, func) do

		func.(head)
		_each(tail, func)

	end

	defp _each(<<>>, _func), do: []

end
Utf8.each "∂og", fn char -> IO.puts char end
```

处理结果：

```elixir
8706
111
103
```

事例代码清晰，同时也有不同的东西值得关注。相比使用`[ head | tail ]`的语法，这里使用了 `<< head::utf8, tail::binary >>`。空列表使用`[]`表示，空二进制则是`<<>>`。


### 熟悉却又陌生

`Elixir`中的字符串基于`Erlang`虚拟机环境有一个长长的进化过程。如果我们从头开始，事情可能会看起来有点不同。但是，一旦你克服了使用字符串二进制文件稍微奇怪的匹配方式，你会发现，它的工作得很好。特别是，模式匹配让寻找特殊序列开头的字符串更容易，也使得解析更简单，更容易书写。

你也许注意到了，本书截至本章，都还没有介绍编程中常用的控制结构，例如`if`和`case`。这是故意为之：`Elixir`相比其他语言，很少用控制结构。当然，并不代表不需要。因此，下一章的主题就是控制结构啦。




